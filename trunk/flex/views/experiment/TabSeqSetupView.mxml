<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas label="Seq application" 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:util="views.util.*" 
	 width="100%" height="100%" show="init()" >
	<mx:states>
		<mx:State name="EditState">
			<mx:RemoveChild target="{hbox1}"/>
			<mx:SetProperty name="enabled" value="true"/>
		</mx:State>
	</mx:states>
<mx:Script>
	<![CDATA[
		import hci.flex.controls.CheckBox;
		
		import mx.collections.XMLListCollection;
		import mx.controls.RadioButton;
		import mx.controls.Spacer;
		import mx.events.ListEvent;
		
		import views.renderers.CheckBoxIsSelectedChipType;
	
		private var validNumberOfSamples:Boolean = true;
		private var validConcentration:Boolean = true;
		private var isValidRange:Boolean = false;
		private var designComplete:Boolean = false;
		private var application:Object = null;
		



		public function init():void {
			checkSecurity();
			
			// We don't show seq apps on edit window
			if (this.currentState == "EditState") {
				return;
			}
			
			// Only initialize once.
			if (this.seqAppBox.getChildren().length > 0) {
				return;
			}
			
			// Map the apps by theme
			var themeMap:Array= new Array();
			for each(var item:Object in parentDocument.filteredAppList) {
				var theKey:Object = item.@idApplicationTheme;
				var apps:XMLListCollection = themeMap[theKey];
				if (apps == null) {
					apps = new XMLListCollection();
					themeMap[theKey] = apps;
				}
				apps.addItem(item);
			}
			
			var rangeBoxSet:Boolean = false;
			var capLibBoxSet:Boolean = false;
			// Organize the sequencing appliction radio buttons by theme.  
			for (var key:Object in themeMap) {
				var theApps:XMLListCollection = themeMap[key];
				
				if (seqAppBox.getChildren().length > 0) {
					var spacer:Spacer = new Spacer();
					spacer.height = 10;
					seqAppBox.addChild(spacer);
				}
				
				
				var header:mx.controls.Label = new mx.controls.Label();
				header.text = parentApplication.dictionaryManager.getEntryDisplay("hci.gnomex.model.ApplicationTheme", key);
				if (header.text != "" && header.text != null) {
					seqAppBox.addChild(header);
				}
				else{
					header.text = "Other";
					seqAppBox.addChild(header);
				}
				
				for each(var app:Object in theApps) {
					var rb:RadioButton = new RadioButton();
					rb.value = app.@value;
					rb.label = app.@application;
					
					rb.group = radioGroupSeqApp;
					rb.setStyle("paddingLeft", 10);
					rb.addEventListener("click", onSeqAppRadioClick);
					seqAppBox.addChild(rb);
					seqAppBox.validateNow();
					
					if(app.@avgInsertSizeFrom != "" && app.@avgInsertSizeTo != "" && !rangeBoxSet ){
						rangeSpacer.height = rb.y;
						rangeSpacer.validateNow();
						rangeBoxSet = true;
					}
					
					if(app.@hasCaptureLibDesign == 'Y' && !capLibBoxSet){
						designSpacer.height = rb.y;
						designSpacer.validateNow();
						capLibBoxSet = true;
					}
			}
		}
	}
		
		public function checkSecurity():void {
		}
		
		private function onSeqAppRadioClick(event:Object):void {
			pickSeqApp();
		}
	
	 

      	public function checkSeqAppSetupCompleteness():Boolean {
        	var isComplete:Boolean = false;
        	if (parentDocument.isEditState() && (isValidRange || designComplete)) {
        		isComplete = true;
        	} else if (parentDocument.isSolexaState() || parentDocument.isSolexaAmendState() || parentDocument.isAmendState()) {
	        	if (this.radioGroupSeqApp.selectedValue != null && this.radioGroupSeqApp.selectedValue != '') {
					
				 	if (insertRanges.visible) {
						// If the avg insert size range is visible, it is a required
						// field, so make sure it has been filled in and passed
						// the range validation.
						if (isValidRange)	{
							isComplete = true;
						} 
					} else if (libraryDesign.visible) {
					
						// If the capture design lib id is visible, it is required, so
						// make sure it has been filled in.
						if (designComplete) {
							isComplete = true;
						}
					} else {
				
						// If neither insert ranges or design lib id is visible, we have
						// passed all of the required field checks already.
						isComplete = true;
						
					}
	        	 }
        	} 

			if (this.currentState != "EditState") {
	        	parentDocument.sampleSetupView.enabled = isComplete; 
        	}
        	
    		parentDocument.enableNextButtons();

        	callLater(postSeqAppSetupCompleteness, [isComplete]);
        	
    		return isComplete;
    	    	
        }
        
        private function postSeqAppSetupCompleteness(isComplete:Boolean):void {
	    		
	    	if (!isComplete) {
	        	if (parentDocument.theTab.contains(parentDocument.sampleSetupView)) {
		        	parentDocument.sampleSetupView.enabled = false;
	        	}
	        	parentDocument.confirmView.enabled = false;
    		} else {
    			if (parentDocument.theTab.contains(parentDocument.sampleSetupView)) {
		    		parentDocument.sampleSetupView.checkSampleSetupCompleteness();
		    	}
    		}
        	
        }

		private function pickSeqApp():void {
			if (this.radioGroupSeqApp.selectedValue != null) {
				this.seqAppBox.styleName = 'normalBox';
				application = parentApplication.dictionaryManager.getEntry('hci.gnomex.model.Application', radioGroupSeqApp.selectedValue);
				if (application != null && application is XMLList) {
					this.parentDocument.applicationName = application[0].@display;
					if(application[0].@hasCaptureLibDesign == 'Y'){ //If it is sure select we need to show the library capture id input box
						libraryDesign.visible = true;
						libraryDesign.includeInLayout = true;
					}
					else{
						libraryDesign.visible = false;
						libraryDesign.includeInLayout = false;
						designID.text = "";
						libraryDesign.styleName = "requiredBox";
						parentDocument.request.@captureLibDesignId = "";
					}
					if(application[0].@avgInsertSizeFrom != "" && application[0].@avgInsertSizeTo != ""){
						insertRanges.visible = true;
						insertRanges.includeInLayout = true;
						insertRangeNote.text = "Insert sizes range from " + application.@avgInsertSizeFrom + " to " + application.@avgInsertSizeTo;
						minRangeValidator.minValue = application.@avgInsertSizeFrom;
						maxRangeValidator.maxValue = application.@avgInsertSizeTo;
					}
					else{
						insertRanges.visible = false;
						insertRanges.includeInLayout = false;
						maxInsertRange.text = "";
						minInsertRange.text = "";
						insertRanges.styleName = "requiredBox";
						parentDocument.request.avgInsertSizeFrom = "";
						parentDocument.request.avgInsertSizeTo = "";
					}
				} else if (application != null && application is XML) {
					this.parentDocument.applicationName = application.@display;
				}
			} else {
				this.seqAppBox.styleName = 'requiredBox';
				this.parentDocument.applicationName = '';
			}
			
			this.checkSeqAppSetupCompleteness();
			if (!this.parentDocument.isAmendState()) {
				this.parentDocument.sampleSetupView.pickSeqApp();
			}
		}
		
		
		private function propagateDesign():void{
			designComplete = false;
			isValidRange = false;
			libraryDesign.styleName = "requiredBox";

			
			if(designID.text != "" || designID.text != null){
				parentDocument.request.@captureLibDesignId = designID.text;
				designComplete = true;
				libraryDesign.styleName = "undefined";
			}
			
			checkSeqAppSetupCompleteness();
		}
		
		private function propagateInsertRange():void{
			
			var pattern:RegExp = /[a-z]/i;
			designComplete = false;
			isValidRange = false;
			insertRanges.styleName = "requiredBox";
			
			if(minInsertRange.text == "" || maxInsertRange.text == "" || pattern.test(minInsertRange.text) || pattern.test(maxInsertRange.text))
				isValidRange = false;
			else if((parseInt(minInsertRange.text) < parseInt(application.@avgInsertSizeFrom) || parseInt(minInsertRange.text) > parseInt(application.@avgInsertSizeTo)) || (parseInt(maxInsertRange.text) > parseInt(application.@avgInsertSizeTo) || parseInt(maxInsertRange.text) < parseInt(application.@avgInsertSizeFrom)))
				isValidRange = false;
			else
			{
				parentDocument.request.@avgInsertSizeFrom = minInsertRange.text;
				parentDocument.request.@avgInsertSizeTo = maxInsertRange.text;
				isValidRange = true;
				insertRanges.styleName = "undefined";
			}	
			checkSeqAppSetupCompleteness();
		}	
	]]>
</mx:Script>
	
	<mx:NumberValidator id="minRangeValidator"
						source="{minInsertRange}"
						lowerThanMinError="Outside valid range."
						exceedsMaxError="Outside valid range."
						property="text"/>
	<mx:NumberValidator id="maxRangeValidator"
						source="{maxInsertRange}"
						lowerThanMinError="Outside valid range."
						exceedsMaxError="Outside valid range."
						property="text"/>

  <mx:XMLListCollection id="seqLibTreatments" source="{parentDocument.request.SeqLibTreatmentEntries.SeqLibTreatment}"/>
  <mx:Canvas width="100%" height="100%">
        	
	<mx:HBox id="mainBox" width="100%" height="100%" verticalGap="2">
   		<mx:VBox height="100%"  paddingTop="2"  verticalGap="2" id="sampleSetupContainer" enabled="true" >
			<mx:Text text="Select a sequencing application."  id="labelApplication"/>
		   <mx:HBox horizontalGap="0" id="hbox1" paddingLeft="12">
		       <mx:RadioButtonGroup id = "radioGroupSeqApp">       	
		       </mx:RadioButtonGroup> 
		       <mx:VBox height="100%" verticalGap="0" paddingLeft="2" paddingRight="2" paddingBottom="0" paddingTop="0" styleName="requiredBox" id="seqAppBox">
		       </mx:VBox>
		   </mx:HBox>
		</mx:VBox>
		
		<mx:VBox  visible="{libraryDesign.visible || insertRanges.visible}" includeInLayout="{libraryDesign.visible || insertRanges.visible}"  height="100%"  paddingTop="2">
			<mx:Spacer id="rangeSpacer" includeInLayout="{insertRanges.visible}"/>
			<mx:VBox verticalGap="0" id="insertRanges" paddingTop="10" paddingLeft="10" visible="false" includeInLayout="false" styleName="requiredBox">
				<mx:HBox id="rangeBox" horizontalGap="0" paddingBottom="10" paddingRight="10">
					<mx:Label text="Average Insert Size: " id="insertRangeLabel" />
					<mx:TextInput id="minInsertRange" width="50" change="propagateInsertRange()"/>
					<mx:Text text=" to "/>
					<mx:TextInput id="maxInsertRange" width="50" change="propagateInsertRange()"/>
					<mx:Text text="bp"/>
				</mx:HBox>
				<mx:Text id="insertRangeNote" fontStyle="italic"/>
			</mx:VBox>
			<mx:Spacer id="designSpacer" includeInLayout="{libraryDesign.visible}"/>
			<mx:VBox verticalGap="0" id="libraryDesign" visible="false" includeInLayout="false" paddingTop="10" paddingLeft="10" styleName="requiredBox">
				<mx:HBox paddingBottom="10" paddingRight="10">
					<mx:Label text="Capture Library Design ID: " id="designIDLabel" />
					<mx:TextInput id="designID" change="propagateDesign()"/>
				</mx:HBox>
			</mx:VBox>
			
		</mx:VBox>
	</mx:HBox>
	
 </mx:Canvas>
</mx:Canvas>