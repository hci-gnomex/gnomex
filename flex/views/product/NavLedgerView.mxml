<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" xmlns:product="views.product.*" xmlns:renderers="views.renderers.*">
	
	
	<mx:HTTPService 
		id="getProductLedgerList" 
		url="GetProductLedgerList.gx"
		resultFormat="e4x"
		concurrency="multiple"
		result="onGetProductLedgerList(event)"
		showBusyCursor="true"
		fault="parentApplication.onFailHttpRequest('Unable to get ledger entries', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:HTTPService 
		id="getProductList" 
		url="GetProductList.gx"
		resultFormat="e4x"
		concurrency="multiple"
		result="onGetProductList(event)"
		showBusyCursor="true"
		fault="parentApplication.onFailHttpRequest('Unable to get product list entries', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	
	
	<mx:Script> 
		<![CDATA[
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			
			
			
			private var selectedIdLab:String;
			private var selectedIdProduct:String;
			
			
			public function init():void{
				getProductLedgerList.send();
				getProductList.send();
			}
			
			private function onGetProductLedgerList(event:ResultEvent):void{
				if(getProductLedgerList.lastResult.name() != 'LedgerList'){
					Alert.show( event.result..ERROR.@message.toString(), "Error getting product ledger list" );	
				} else{
					
					for each(var lab:XML in browseTree.dataProvider){
						browseTree.expandItem(lab, true, false);
					}
					
					if(selectedIdLab != null && selectedIdProduct != null){
						reselectTreeItem();
					}
					
				}
				
			}
			
			private function onGetProductList(event:ResultEvent):void{
				if(getProductList.lastResult.name() != 'ProductList'){
					Alert.show( event.result..ERROR.@message.toString(), "Error getting product list" );	
				} else{
					var emptyNode:XML = <Product name="" idProduct="" />
					filteredProducts.addItemAt(emptyNode, 0);
					productCombo.selectedIndex = -1;
				}
			}
			
			private function onBrowseTree():void{
				if(browseTree.selectedItem == null){
					return;
				}
				
				if(browseTree.selectedItem.name() == "product"){
					selectedIdLab = browseTree.selectedItem.@idLab;
					selectedIdProduct = browseTree.selectedItem.@idProduct;
					productEntryLedgerView.idLab = browseTree.selectedItem.@idLab;
					productEntryLedgerView.idProduct = browseTree.selectedItem.@idProduct;
					productEntryLedgerView.productCount = browseTree.selectedItem.@qty;
					productEntryLedgerView.productName = browseTree.selectedItem.@display;
					productEntryLedgerView.init();
				} else{
					productEntryLedgerView.idLab = null;
					productEntryLedgerView.idProduct = null;
					productEntryLedgerView.productCount = "";
					productEntryLedgerView.productName = "";
					productEntryLedgerView.init();
					
				}
			}
			
			private function reselectTreeItem():void{
				var index:int = 2;
				var found:Boolean = false;
				for each(var lab:XML in browseTree.dataProvider){
					index++;
					if(lab.@idLab == selectedIdLab){
						for each(var product:XML in lab.children()){
							index++;
							if(product.@idProduct == selectedIdProduct){
//								browseTree.selectedIndex = index;
								browseTree.selectedItem = product;
								found = true;
								break;
							}
						}	
					}
					
					if(found){
						break;
					}
				}
				
				onBrowseTree();
			}
			
			private function filterLedgerList():void{
				var params:Object = new Object();
				
				if(browseLabCombo.selectedIndex >= 0){
					params.idLab = browseLabCombo.selectedItem.@idLab;
				}
				
				if(productCombo.selectedIndex > 0){
					params.idProduct = productCombo.selectedItem.@idProduct;
				}
				
				getProductLedgerList.send(params);
			}
			
			private function resetProductCombo():void {
				if (productCombo.selectedIndex == 0) {
					productCombo.selectedIndex = -1;
					productCombo.selectedItem = null;
				}
			}
			
			private function filterProducts(item:Object):Boolean {
				if (item.@name == '' && item.@idProduct == '') {
					return true;
				}
				
				if (item.@idProductType != null && item.@idProductType != '') {
					for each (var productType:Object in filteredProductTypes) {
						if (productType.@value == item.@idProductType) {
							return true;
						}
					}
				}
				
				return false;
			}
			
			private function filterProductTypes(item:Object):Boolean {
				if (item.@value != null && item.@value != '') {
					if (parentApplication.coreFacilitiesICanManage != null && parentApplication.coreFacilitiesICanManage.length() > 0) {
						for each (var coreFacility1:Object in parentApplication.coreFacilitiesICanManage) {
							if (coreFacility1.@idCoreFacility == item.@idCoreFacility) {
								return true;
							}
						}
					} else if (parentApplication.myCoreFacilities != null && parentApplication.myCoreFacilities.length() > 0) {
						for each (var coreFacility2:Object in parentApplication.myCoreFacilities) {
							if (coreFacility2.@idCoreFacility == item.@idCoreFacility) {
								return true;
							}
						}
					}
				}
				
				return false;
			}
						
			public function addButtonClick():void {
				var addProductWindow:AddProductWindow = new AddProductWindow();
				addProductWindow.parentWindow = this;
				addProductWindow.products = this.products;
				addProductWindow.getCoreFacilityLabList.send();
				
				PopUpManager.addPopUp(addProductWindow,this, true)
				PopUpManager.centerPopUp(addProductWindow);
				this.setFocus();
			}
			
			public function addProductToTree(labToGetProduct:Object, productToAdd:Object):void{
				var index:int = 2;
				var found:Boolean = false;
				for each(var lab:XML in browseTree.dataProvider){
					index++;
					if(lab.@idLab == labToGetProduct.@idLab){
						for each(var product:XML in lab.children()){
							index++;
							if(product.@idProduct == productToAdd.@idProduct){
//								browseTree.selectedIndex = index;
								browseTree.selectedItem = product;
								found = true;
								break;
							}
						}
						if (!found) {
							lab.appendChild(productToAdd);
							browseTree.selectedItem = productToAdd;
							found = true;
						}
					}
					
					if(found){
						break;
					}
				}
				
				if (!found) {
					labToGetProduct.appendChild(productToAdd);
					labs.addItem(labToGetProduct);
					browseTree.expandItem(labToGetProduct, true, false);
					browseTree.selectedItem = productToAdd;
				}
				
				onBrowseTree();
			}
			
		]]>
	</mx:Script> 
	
	
	
	<mx:XMLListCollection id="labs" source="{getProductLedgerList.lastResult..Lab}"/>
	<mx:XMLListCollection id="products" source="{getProductList.lastResult..Product}" />
	<mx:XMLListCollection id="filteredProducts" source="{getProductList.lastResult..Product}" filterFunction="filterProducts"/>
	<mx:XMLListCollection id="filteredProductTypes" source="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.ProductType').DictionaryEntry}" filterFunction="filterProductTypes"/>
	
	<mx:VBox id="vbox1" width="100%" height="100%" horizontalAlign="left" verticalGap="2">
		<mx:HBox width="100%" styleName="titleBox">
			<renderers:TitleLabel iconSource="{parentApplication.iconBasket}" titleText="Product Ledgers" />
			<mx:HBox horizontalGap="10" paddingTop="0" paddingLeft="0" horizontalAlign="left" verticalAlign="middle" width="100%">
				
				<mx:HBox verticalAlign="middle"  
						 visible="{parentApplication.submitRequestLabList.source.length() &gt; 1}"
						 includeInLayout="{parentApplication.submitRequestLabList.source.length() &gt; 1}">
					<mx:Label text="Lab:"/>
					<renderers:FilterComboBox dataProvider="{parentApplication.submitRequestLabList.source}"
											  editable="{parentApplication.submitRequestLabList.source.length() &gt; 1}"
											  enabled="{parentApplication.submitRequestLabList.source.length() &gt; 1}"
											  labelField="@name" 
											  prompt="Select a lab..."
											  width="160"  
											  selectedIndex="{parentApplication.submitRequestLabList.source.length() == 1 ? 0 : -1}"
											  id="browseLabCombo" fontSize="10"
											  close="{this.filterLedgerList()}"/>
				</mx:HBox>
								
				<mx:Spacer width="5"/>
				<mx:Label text="Product:"/>
				<mx:ComboBox id="productCombo" dataProvider="{this.filteredProducts}" labelField="@name" close="{this.resetProductCombo(); this.filterLedgerList()}" selectedIndex="-1" fontSize="10"/>
			</mx:HBox>
		</mx:HBox>
		
		<mx:HDividedBox height="100%" width="100%" dividerAlpha="0" dividerThickness="3" dividerAffordance="0">	
			
			<mx:VBox styleName="panelBox" id="navPanel" height="100%" width="270">
				
				<mx:HBox width="100%" verticalAlign="middle" height="26">
					
					<mx:Spacer width="100%" height="100%" />
					<mx:LinkButton label="Add product"
								   icon="@Embed('../../assets/add.png')"
								   click="{addButtonClick()}"
								   id="addProductButton"
								   textDecoration="underline"
								   visible="{parentApplication.isAdminState}"
								   includeInLayout="{parentApplication.isAdminState}"
								   color="#000000"  fontSize="10"/>
				</mx:HBox>
				<mx:VBox styleName="panelBox" height="100%" width="100%" >
					<mx:Tree width="100%" height="100%"  
							 dataProvider="{labs}" 
							 showRoot="false"
							 id="browseTree" click="onBrowseTree();" 
							 doubleClickEnabled="false" 
							 labelField="@display" allowMultipleSelection="false" toolTip="Each lab's products available in inventory and pending in product order(s)"
							 iconFunction="parentApplication.getTreeIcon" backgroundAlpha="1" dropShadowEnabled="true" borderStyle="none">
					</mx:Tree>
					
				</mx:VBox>
			</mx:VBox>
			
			
			<!-- Box 2 -->
			<mx:VBox height="100%" width="100%">
				<product:ProductLedgerView id="productEntryLedgerView" width="100%" height="100%" />
			</mx:VBox>
			
		</mx:HDividedBox>
	</mx:VBox>
	
</mx:Canvas>
