<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="vertical" 
	title="Add Analysis"
	initialize="init()"
	xmlns:util="views.util.*" 
	xmlns:renderers="views.renderers.*" 
	defaultButton="{saveButton}" width="466" horizontalScrollPolicy="off" verticalScrollPolicy="off">
	 
    <mx:HTTPService 
    id="saveAnalysis" 
    url="SaveAnalysis.gx"
    resultFormat="e4x"
    result="onSaveAnalysis(event)"
    fault="parentApplication.onFailHttpRequest('Unable to save analysis', event)"
    method="POST"
    useProxy="false">
    </mx:HTTPService >
    
    <mx:HTTPService 
    id="getAnalysis" 
    url="GetAnalysis.gx"
    destination="getAnalysis"
    showBusyCursor="true"
    resultFormat="e4x"
    result="onGetAnalysis(event)"
    fault="parentApplication.onFailHttpRequest('Unable to get analysis', event)"
    useProxy="false">   
     
    </mx:HTTPService >    
      <mx:HTTPService  
    id="getAnalysisGroupList" 
    url="GetAnalysisGroupList.gx"
    destination="getAnalysisGroupList"
    resultFormat="e4x"
    showBusyCursor="true"
    result="onGetAnalysisGroupList(event)"
 	fault="parentApplication.onFailHttpRequest('Failed to get analysis group list', event)"
    method="POST"   
    useProxy="false">
    <mx:request>
            <labKeys>{labCombo.selectedItem.@idLab}</labKeys> 
    </mx:request> 
   </mx:HTTPService>
	
	<mx:HTTPService             
		id="getLab" 
		url="GetLab.gx" 
		destination="getLab" 
		resultFormat="e4x"
		result="onGetLab(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get lab', event)"
		method="POST" 
		useProxy="false">
	</mx:HTTPService> 


<mx:Script>
	<![CDATA[
	import mx.collections.Sort;
	import mx.collections.XMLListCollection;
	import mx.controls.Alert;
	import mx.events.CloseEvent;
	import mx.managers.PopUpManager;
	import mx.rpc.events.FaultEvent;
	import mx.rpc.events.ResultEvent;

	import views.experiment.NavExperimentView;
	import views.util.AboutView;
	import views.util.DateUtils;
	import views.util.GNomExStringUtil;
	import views.util.OrganismEvent;
	import views.util.OrganismsWindow;
	import views.util.PromptXMLListCollection;

	public var parentView:Object;

	private var savedIdAnalysis:Object;
	private var savedIdAnalysisGroup:Object;

	private var selectedIdAnalysisGroup:Object;
	private var selectedIdLab:Object;

	[Bindable]
	private var appUsersForLab:XMLListCollection;
	private var appUserSort:Sort;

	private var idLab:String;

	[Bindable]
	private var isAdmin:Boolean = false;

	[Bindable]
	private var possibleInstitutions:PromptXMLListCollection;

	private function init():void {
		if (parentApplication.hasPermission("canWriteAnyObject")) {
			isAdmin = true;
		}
	}

	public function initializeData(idLab:Object, idAnalysisGroup:Object, idOrganism:Object):void {
		initializeDataProviders();
		selectedIdLab = idLab;
		selectedIdAnalysisGroup = idAnalysisGroup;
		// Select the lab in the dropdown
		var foundLab:Boolean = false;
		if (selectedIdLab != null && selectedIdLab != '') {
			for each(var l:Object in labCombo.dataProvider) {
				if (l.@idLab == selectedIdLab) {
					labCombo.selectedItem = l;
					var params:Object = new Object();
					params.labKeys = l.@idLab;
					getAnalysisGroupList.send(params);
					foundLab = true;
					break;
				}
			}
		} else if (labCombo.dataProvider.length == 1) {
			labCombo.selectedItem = labCombo.dataProvider[0];
			var params2:Object = new Object();
			params2.labKeys = labCombo.dataProvider[0].@idLab;
			getAnalysisGroupList.send(params2);
			foundLab = true;
		}

		if (!foundLab) {
			labCombo.selectedIndex = -1;
			getAnalysisGroupList.send();
		}
		callGetLab();

		var defaultVisibility:String = parentApplication.getProperty(parentApplication.PROPERTY_DEFAULT_VISIBILITY_ANALYSIS);
		if (defaultVisibility != null && defaultVisibility.length > 0) {
			for each(var item:Object in parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry) {
				if (item.@value == defaultVisibility) {
					this.visibilityCombo.selectedItem = item;
					break;
				}
			}
		}

		parentApplication.removeEventListener(OrganismEvent.DATA_REFRESHED,
				onOrganismRefreshed);
		parentApplication.addEventListener(OrganismEvent.DATA_REFRESHED,
				onOrganismRefreshed);

		if (labCombo.selectedItem != null) {
			// Set up institutions
			var emptyInstNode:XML = <Institution idInstitution='' display=''/>;
			possibleInstitutions = new PromptXMLListCollection(emptyInstNode, new XMLListCollection(labCombo.selectedItem.institutions.Institution.(@isActive == 'Y')));
			this.institutionCombo.dataProvider = possibleInstitutions;
			visibilityChosen();
		}

		if (idOrganism != null && idOrganism != '') {
			for each (var org:Object in organismCombo.dataProvider) {
				if (org.@idOrganism == idOrganism) {
					organismCombo.selectedItem = org;
					filteredGenomeBuildList.refresh();
					break;
				}
			}
		}
	}

	private function onOrganismRefreshed(event:Event):void {
		filteredGenomeBuildList.refresh();
	}

	private function initializeDataProviders():void {
		filteredGenomeBuildList.refresh();

	}

	public function refreshAnalysisGroupForDropdown():void {
		if (this.labCombo.selectedItem != null && this.labCombo.selectedItem.@idLab != '') {
			if (selectedIdAnalysisGroup != null) {
				for each(var ag:Object in analysisGroupList) {
					if (ag.@idAnalysisGroup == selectedIdAnalysisGroup) {
						this.analysisGroupCombo.selectedItem = ag;
						selectedIdAnalysisGroup = null;
						break;
					}
				}

			}
		} else {
			analysisGroupList = new XMLListCollection();
		}
	}

	public function selectAnalysisGroupDropdown(idAnalysisGroup:Object):void {
		for each(var ag:Object in analysisGroupList) {
			if (ag.@idAnalysisGroup == idAnalysisGroup) {
				this.analysisGroupCombo.selectedItem = ag;
				break;
			}
		}
	}

	private function validateAndSave():void {
		if (isAdmin && (ownerCombo.selectedItem == null || ownerCombo.selectedIndex == 0)) {
			ownerCombo.setFocus();
			Alert.show("Please select the analysis owner.");
			return;
		}
		if (genomeBuilds.length == 0) {
			var outMsg:String = "You have selected a genome build without adding it to the genome build list.  Create new analysis anyway?";
			if (genomeBuildCombo.selectedItem == null || genomeBuildCombo.selectedItem.@value == "") {
				outMsg = "A genome build has not been specified.  Create new analysis anyway?";
			}
			Alert.show(outMsg,
					"Warning",
					(Alert.YES | Alert.NO), this,
					onPromptToContinueSave,
					parentApplication.iconWarning);
			return;
		}
		save();
	}

	private function onPromptToContinueSave(event:CloseEvent):void {
		if (event.detail == Alert.YES) {
			save();
		}
	}

	private function save():void {
		var params:Object = new Object();

		params.idAnalysis = 0;
		params.idLab = labCombo.selectedItem.@idLab;
		params.codeVisibility = visibilityCombo.selectedItem.@value;
		params.idInstitution = institutionCombo.selectedItem == null ? '' : institutionCombo.selectedItem.@idInstitution;
		if (isAdmin) {
			params.idAppUser = ownerCombo.selectedItem.@idAppUser;
		}
		params.name = analysisName.text;
		if (organismCombo.selectedItem == null) {
			Alert.show("Please select an organism",
					"Message",
					(Alert.OK));
			return;
		}
		params.idOrganism = organismCombo.selectedItem.@idOrganism;
		//params.idGenomeBuild = genomeBuildCombo.selectedItem != null ? genomeBuildCombo.selectedItem.@idGenomeBuild : "";
		params.idGenomeBuild = "";
		params.genomeBuildsXMLString = "<genomeBuilds>" + genomeBuilds.toXMLString() + "</genomeBuilds>";

		if (currentState == "NewAnalysisGroupState") {
			params.analysisGroupsXMLString = "<analysisGroups/>";
			params.newAnalysisGroupName = newAnalysisGroupName.text;

		} else {
			if (analysisGroupCombo.selectedItem != null) {
				params.analysisGroupsXMLString = "<analysisGroups>" + analysisGroupCombo.selectedItem.toXMLString() + "</analysisGroups>";
			} else {
				params.analysisGroupsXMLString = "<analysisGroups/>";
			}
		}
		if (parentApplication.isPrivacyExpirationSupported) {
			var expireDate:Date = DateUtils.dateAdd(DateUtils.MONTH, parseInt(parentApplication.getProperty(parentApplication.PROPERTY_DATASET_PRIVACY_EXPIRATION)), new Date());
			params.privacyExpirationDate = parentApplication.formatDate(expireDate);
		}
		params.analysisFilesXMLString = "<analysisFiles/>";
		params.analysisFilesToDeleteXMLString = "<analysisFilesToDelete/>";
		params.collaboratorsXMLString = "<collaborators/>";

		// If this window was called from experiment browse, automatically link selected experiment to analysis
		if (isNavExperimentViewOpen() && parentView.selectedExperiment != null) {
			var hybs:XMLListCollection = new XMLListCollection();
			var lanes:XMLListCollection = new XMLListCollection();
			var samples:XMLListCollection = new XMLListCollection();

			for each (var seqLane:Object in parentView.selectedExperiment.sequenceLanes.children()) {
				addExperimentItem(seqLane, hybs, lanes, samples);
			}
			for each (var hyb:Object in parentView.selectedExperiment.hybridizations.children()) {
				addExperimentItem(hyb, hybs, lanes, samples);
			}
			for each (var sample:Object in parentView.selectedExperiment.samples.children()) {
				addExperimentItem(sample, hybs, lanes, samples);
			}

			params.hybsXMLString = "<hybs>" + hybs.toXMLString() + "</hybs>";
			params.lanesXMLString = "<lanes>" + lanes.toXMLString() + "</lanes>";
			params.samplesXMLString = "<samples>" + samples.toXMLString() + "</samples>";
		} else {
			params.hybsXMLString = "<hybs/>";
			params.lanesXMLString = "<lanes/>";
			params.samplesXMLString = "<samples/>";
		}

		saveAnalysis.send(params);
	}

	private function onSaveAnalysis(event:ResultEvent):void {
		if (saveAnalysis.lastResult.name() == "SUCCESS") {
			savedIdAnalysis = saveAnalysis.lastResult.@idAnalysis;
			savedIdAnalysisGroup = saveAnalysis.lastResult.@idAnalysisGroup;

			var params:Object = new Object();
			params.idAnalysis = savedIdAnalysis;
			getAnalysis.send(params);
			PopUpManager.removePopUp(this);

			if (isNavExperimentViewOpen()) {
				parentApplication.showAnalysisForNumber('A' + savedIdAnalysis);
			}

		} else {
			Alert.show(saveAnalysis.lastResult..ACTMESSAGE.@TEXT);
		}
	}

	private function checkCompleteness():Boolean {
		var isComplete:Boolean = false;
		if (currentState == "NewAnalysisGroupState") {
			isComplete = labCombo.selectedItem != null &&
					labCombo.selectedItem.@idLab != ""
			newAnalysisGroupName.text != '' &&
			analysisName.text != '' &&
			organismCombo.selectedItem != null &&
			organismCombo.selectedItem.@value != "" &&
			ownerCombo.selectedItem != null &&
			ownerCombo.selectedItem.@idAppUser != "" &&
			institutionNote.visible == false &&
			visibilityCombo.selectedItem != null &&
			visibilityCombo.selectedItem.@value != "";

		} else {
			isComplete = labCombo.selectedItem != null &&
					labCombo.selectedItem.@idLab != ""
			analysisName.text != '' &&
			organismCombo.selectedItem != null &&
			organismCombo.selectedItem.@value != "" &&
			ownerCombo.selectedItem != null &&
			ownerCombo.selectedItem.@idAppUser != "" &&
			institutionNote.visible == false &&
			visibilityCombo.selectedItem != null &&
			visibilityCombo.selectedItem.@value != "";
		}
		this.saveButton.enabled = isComplete;
		return isComplete;
	}


	private function onGetAnalysis(event:ResultEvent):void {
		if (isNavAnalysisViewOpen()) {
			parentApplication.navAnalysisView.selectedIdAnalysis = savedIdAnalysis;
			parentApplication.navAnalysisView.selectedAnalysis = getAnalysis.lastResult.Analysis.(@idAnalysis = savedIdAnalysis);
			if (savedIdAnalysisGroup != "-1") {
				parentApplication.navAnalysisView.selectedIdAnalysisGroup = savedIdAnalysisGroup;
			} else {
				parentApplication.navAnalysisView.selectedIdAnalysisGroup = analysisGroupCombo.selectedItem.@idAnalysisGroup;
			}
			parentApplication.navAnalysisView.forceReload = true;
			parentApplication.navAnalysisView.showEditView = true;
			parentApplication.navAnalysisView.refreshAnalysis();
			parentApplication.navAnalysisView.updateLabList();
		}
	}

	private function refreshAnalysisGroups():void {
		if (isNavAnalysisViewOpen()) {
			if (parentApplication.navAnalysisView.currentState == "AdminState" && labCombo.selectedItem != null) {
				parentApplication.navAnalysisView.labKeys = labCombo.selectedItem.@idLab;
				parentApplication.navAnalysisView.selectedLabsLabel = labCombo.selectedItem.@name;
				callGetLab();
			}
			if (labCombo.selectedItem != null && labCombo.selectedItem != '') {
				parentApplication.navAnalysisView.selectedIdLab = labCombo.selectedItem.@idLab;
			}
			callLater(parentApplication.navAnalysisView.refreshAnalysis);
		} else {
			if (isAdmin && labCombo.selectedItem != null && labCombo.selectedItem.@idLab != '') {
				callGetLab();
			}
		}
		ownerCombo.selectedIndex = -1;
		getAnalysisGroupList.send();
	}

	private function callGetLab():void {
		if (isAdmin) {
			var params:Object = new Object();
			idLab = labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : "";
			if (idLab.length > 0) {
				params.idLab = idLab;
				params.includeBillingAccounts = "N";
				params.includeProductCounts = "N";
				params.includeSubmitters = "N";
				params.includeMoreCollaboratorInfo = "N";
				params.includeHistoricalOwnersAndSubmitters = "N";
				getLab.send(params);
			}
		}
	}

	private function onGetLab(event:ResultEvent):void {
		if (getLab.lastResult.name() == "OpenLabList") {
			var lab:Object = getLab.lastResult..Lab.(@idLab == idLab);
			var emptyNode:XML = <AppUser idAppUser='' displayName=''/>;
			appUsersForLab = new XMLListCollection(lab.members.AppUser.(@isActive == 'Y'));
			for each (var mgr:Object in lab.managers.AppUser) {
				if (appUsersForLab.source.(@idAppUser == mgr.@idAppUser).length() == 0) {
					if (mgr.@isActive == 'Y') {
						appUsersForLab.addItem(mgr);
					}
				}
			}
			appUsersForLab.addItemAt(emptyNode, 0);

			appUserSort = new Sort();
			appUserSort.compareFunction = GNomExStringUtil.sortAppUsers;
			appUsersForLab.sort = appUserSort;
			appUsersForLab.refresh();
			ownerCombo.dataProvider = appUsersForLab;
			ownerCombo.selectedItem = emptyNode;

			var a:Object;
			// Set default owner to current user
			for each(a in appUsersForLab) {
				if (a.@idAppUser == parentApplication.idAppUser) {
					ownerCombo.selectedItem = a;
					break;
				}
			}

			// Set up institutions
			var emptyInstNode:XML = <Institution idInstitution='' display=''/>;
			possibleInstitutions = new PromptXMLListCollection(emptyInstNode, new XMLListCollection(lab.institutions.Institution.(@isActive == 'Y')));
			this.institutionCombo.dataProvider = possibleInstitutions;
			visibilityChosen();
		}
	}

	private function onGetAnalysisGroupList(event:ResultEvent):void {
		this.refreshAnalysisGroupForDropdown();
	}

	private function filterGenomeBuild(item:Object):Boolean {
		if (item.@value == "") {
			return true;
		}
		if (item.@isActive == 'N') {
			return false;
		}
		var doesMatch:Boolean = false;
		if (this.organismCombo.selectedItem != null &&
				this.organismCombo.selectedItem.@value != null &&
				this.organismCombo.selectedItem.@value != "") {
			if (item.@idOrganism.toString() == this.organismCombo.selectedItem.@value.toString()) {
				doesMatch = true;
			}
		}
		return doesMatch;
	}

	//
	// Edit organism dictionary
	//
	private function editOrganism():void {
		var configWindow:OrganismsWindow = OrganismsWindow(PopUpManager.createPopUp(parentApplication.theBody, OrganismsWindow, true));
		PopUpManager.centerPopUp(configWindow);
		if (organismCombo.selectedItem != null) {
			configWindow.selectOrganism(organismCombo.selectedItem.@idOrganism);
		}
	}

	private function addGenomeBuild():void {
		var node:XML = new XML("<GenomeBuild " +
				" idGenomeBuild='" + genomeBuildCombo.selectedItem.@idGenomeBuild + "'" +
				" display='" + genomeBuildCombo.selectedItem.@display + "'" +
				"/>");
		this.genomeBuilds.addItem(node);
	}

	private function removeGenomeBuild():void {
		if (genomeBuildGrid.selectedIndex != -1) {
			genomeBuilds.removeItemAt(genomeBuildGrid.selectedIndex);
		}
	}

	private function visibilityChosen():void {
		if (this.labCombo.selectedItem != null && this.labCombo.selectedItem.@idLab != '' && this.visibilityCombo.selectedItem != null && this.visibilityCombo.selectedItem.@value == "INST") {
			var lab:Object = labCombo.selectedItem;
			this.institutionCombo.enabled = true;
			this.institutionLabel.enabled = true;
			if (this.institutionCombo.selectedItem == null || this.institutionCombo.selectedItem.@idInstitution == '') {
				for each(var inst:Object in this.possibleInstitutions) {
					if (inst.@idInstitution == lab.@defaultIdInstitutionForLab) {
						this.institutionCombo.selectedItem = inst;
						break;
					}
				}
			}
		} else {
			this.institutionCombo.enabled = false;
			this.institutionLabel.enabled = false;
			this.institutionCombo.selectedItem = null;
		}
	}

	private function addExperimentItem(item:Object, hybs:XMLListCollection, lanes:XMLListCollection, samples:XMLListCollection):void {
		if (item.name() == "SequenceLane") {
			var found:Boolean = false;
			for each (var lane:Object in lanes) {
				if (lane.@number == item.@number) {
					found = true;
					break;
				}
			}
			if (!found) {
				lanes.addItem(item);
			}
		} else if (item.name() == "Hybridization") {
			for each (var hyb:Object in hybs) {
				if (hyb.@number == item.@number) {
					found = true;
					break;
				}
			}
			if (!found) {
				item.@idRequest = parentView.selectedExperiment.@idRequest;
				hybs.addItem(item);
			}
		} else if (item.name() == "Sample") {
			for each (var sample:Object in samples) {
				if (sample.@number == item.@number) {
					found = true;
					break;
				}
			}
			if (!found) {
				samples.addItem(item);
			}
		}
	}

	private function isNavAnalysisViewOpen():Boolean {
		if (parentView != null && parentView is NavAnalysisView) {
			return true;
		}
		return false;
	}

	private function isNavExperimentViewOpen():Boolean {
		if (parentView != null && parentView is NavExperimentView) {
			return true;
		}
		return false;
	}
	]]>
</mx:Script>
<mx:XMLListCollection id="analysisGroupList" source="{getAnalysisGroupList.lastResult..AnalysisGroup}"/>
<mx:XMLListCollection  id="filteredGenomeBuildList" source="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.GenomeBuildLite').DictionaryEntry}"  filterFunction="filterGenomeBuild"/>
<mx:XMLListCollection  id="organisms" source="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.OrganismLite').DictionaryEntry}" />
<mx:XMLListCollection id="genomeBuilds"/>
		<mx:VBox height="100%" verticalGap="0" width="100%">
			<mx:HBox verticalGap="0" horizontalGap="0">
				<mx:VBox verticalGap="0">
					<mx:Label width="115" text="Group"/>
				</mx:VBox>								
				<mx:VBox verticalGap="0">
					<renderers:FilterComboBox dataProvider="{parentApplication.submitRequestLabList}"
											  labelField="@name" 
											  prompt="Select a lab..."
											  width="336"
											  close="refreshAnalysisGroups();checkCompleteness()"
											  id="labCombo"/>
				</mx:VBox>
			</mx:HBox>				
		</mx:VBox>		
		<mx:VBox height="100%" verticalGap="0" width="100%" id="analysisOwnerBox" 
				 visible="{isAdmin}" 
				 includeInLayout="{isAdmin}">
			<mx:HBox verticalGap="0" horizontalGap="0">
				<mx:Label width="115" text="Analysis owner"/>						
				<util:ComboBoxDatProvChangeFix  width="336" id="ownerCombo" labelField="@displayName" dataProvider="{appUsersForLab}" close="checkCompleteness()"></util:ComboBoxDatProvChangeFix>
			</mx:HBox>				
		</mx:VBox>
		<mx:VBox height="100%" verticalGap="0" width="100%" id="analysisGroupBox">
			<mx:HBox verticalGap="0" horizontalGap="0">
				<mx:VBox verticalGap="-5">
					<mx:Label width="115" text="Analysis group" id="label1"/>
					<mx:HBox width="100%" id="hbox1">
						<mx:LinkButton label="New..." textDecoration="underline" color="#161FDA" id="newAnalysisGroupButton" click="currentState = &quot;NewAnalysisGroupState&quot;"  paddingTop="0" paddingBottom="0" paddingLeft="0" paddingRight="0"/>
					</mx:HBox>
				</mx:VBox>								
				<mx:VBox verticalGap="0">
					<renderers:FilterComboBox width="336" id="analysisGroupCombo"
								 dataProvider="{analysisGroupList}" 
								 labelField="@name" selectedIndex="-1" prompt="..." change="checkCompleteness()"/>
				</mx:VBox>
			</mx:HBox>				
		</mx:VBox>		
		<mx:VBox height="100%" verticalGap="0" width="100%">
			<mx:HBox verticalGap="0" horizontalGap="0">
				<mx:VBox verticalGap="0">
					<mx:Label width="115" text="Analysis name"/>
				</mx:VBox>								
				<mx:VBox verticalGap="0">
					<mx:TextInput id="analysisName" width="336" change="checkCompleteness()"/>
				</mx:VBox>
			</mx:HBox>				
		</mx:VBox>		
		<mx:VBox height="100%" verticalGap="0" width="100%">
			<mx:HBox verticalGap="0" horizontalGap="0">
				<mx:VBox verticalGap="-5">
					<mx:Label text="Organism" width="115" fontWeight="normal">
					</mx:Label>
					<mx:LinkButton label="New/Edit..." textDecoration="underline" color="#0F19DF" click="editOrganism()" paddingTop="0" paddingBottom="0" paddingLeft="0" paddingRight="0"/>
				</mx:VBox>								
				<mx:VBox verticalGap="0">
					<renderers:FilterComboBox labelField="@display"
											  prompt="Organism..."
											  close="filteredGenomeBuildList.refresh();checkCompleteness();"
											  id="organismCombo"
											  dataProvider="{parentApplication.activeOrganismList}"
											  width="336"/>
				</mx:VBox>
			</mx:HBox>				
		</mx:VBox>
		<mx:VBox height="100%" verticalGap="0" width="100%">
			<mx:HBox width="100%" verticalGap="0" horizontalGap="0">
				<mx:VBox verticalGap="-5">
					<mx:Label text="Genome Build(s)" width="115" fontWeight="normal">
					</mx:Label>
					<mx:LinkButton label="New/Edit..." textDecoration="underline" color="#0F19DF" click="editOrganism()" paddingBottom="0" paddingLeft="0" paddingRight="0" paddingTop="0"/>
				</mx:VBox>
				<mx:VBox verticalGap="0">
					<mx:Label styleName="hintText" text="(Select from dropdown, use '+' to add to list below)" width="296" fontWeight="normal">
					</mx:Label>																			
					<mx:HBox verticalGap="0" horizontalGap="2" width="350">
						<renderers:FilterComboBox width="281" id="genomeBuildCombo" labelField="@display"  change="checkCompleteness()"
							dataProvider="{filteredGenomeBuildList}"/>
						<mx:Button 
							enabled="{genomeBuildCombo.selectedItem != null &amp;&amp; genomeBuildCombo.selectedIndex > 0 ? true : false}" 
							icon="@Embed(source='../../assets/add.png')" 
							disabledIcon="@Embed(source='../../assets/add_disable.png')"
							click="addGenomeBuild()" 
							width="25">
						</mx:Button>
						<mx:Button  
							icon="@Embed(source='../../assets/delete.png')" 
							disabledIcon="@Embed(source='../../assets/delete_disable.png')" 
							enabled="{genomeBuildGrid.selectedItem != null}" 
							click="removeGenomeBuild()" 
							width="25"/>
					</mx:HBox>
				</mx:VBox>
			</mx:HBox>
			<mx:HBox width="451" verticalGap="0" horizontalGap="0" horizontalAlign="right">
				<mx:DataGrid id="genomeBuildGrid" width="335" height="43" dataProvider="{genomeBuilds}" showHeaders="false">
					<mx:columns>
						<mx:DataGridColumn dataField="@display"/>
					</mx:columns>
				</mx:DataGrid>	
			</mx:HBox>
		</mx:VBox>
		<mx:VBox height="100%" verticalGap="0" width="100%">
			<mx:HBox width="100%" verticalGap="0" horizontalGap="0">
				<mx:Label text="Visibility" width="118"/>
				<mx:ComboBox id="visibilityCombo" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry}" labelField="@display" width="281" close="{visibilityChosen()}"/>
			</mx:HBox>
			<mx:Text id="institutionNote" paddingLeft="30" styleName="submitNote" visible="{this.visibilityCombo.selectedItem.@display == 'Institution' &amp;&amp; this.labCombo.selectedItem..institutions.children().length() == 0 }" paddingBottom="0">									
				<mx:text>Institution visibility can only be set when lab belongs to an institution.</mx:text>
			</mx:Text>
			<mx:HBox horizontalGap="0" verticalGap="0" width="100%">
				<mx:Label text="Institution" width="118" id="institutionLabel"/>
				<mx:ComboBox id="institutionCombo" labelField="@display" width="281" enabled="false"/>
			</mx:HBox>
		</mx:VBox>
		<mx:ControlBar width="451" horizontalAlign="right">
			<mx:Button label="Save" id="saveButton" click="validateAndSave()" icon="@Embed(source='../../assets/save.png')" 
					   enabled="{analysisName.text != '' &amp;&amp; analysisGroupCombo.selectedItem != null &amp;&amp; labCombo.selectedItem != null &amp;&amp; !institutionNote.visible &amp;&amp; visibilityCombo.selectedItem != null &amp;&amp; 
					   			visibilityCombo.selectedItem.@value != '' &amp;&amp; organismCombo.selectedItem != null &amp;&amp; organismCombo.selectedItem.@value != ''}" 
					   disabledIcon="@Embed(source='../../assets/save_disable.png')"/>
			<mx:Button label="Cancel" click="{mx.managers.PopUpManager.removePopUp(this)}"/>
		</mx:ControlBar>				


	
	
	<mx:states>
		<mx:State name="NewAnalysisGroupState">
			<mx:RemoveChild target="{analysisGroupCombo}"/>
			<mx:RemoveChild target="{newAnalysisGroupButton}"/>
			<mx:SetProperty target="{label1}" name="text" value="New analysis group name"/>
			<mx:SetProperty target="{label1}" name="width" value="200"/>
			<mx:AddChild relativeTo="{analysisGroupBox}" position="lastChild">
				<mx:TextInput width="100%" id="newAnalysisGroupName" change="checkCompleteness()"/>
			</mx:AddChild>
			<mx:RemoveChild target="{hbox1}"/>
		
		</mx:State>
	</mx:states>
</mx:TitleWindow>
