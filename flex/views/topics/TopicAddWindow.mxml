<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
	title="{windowTitle}"
	showCloseButton="true" 
	width="440" 
	height="162"
	verticalGap="0"
	close="closeWindow()"
	titleIcon="{parentApplication.iconTopic}"
	xmlns:util="views.util.*" 
	xmlns:renderers="views.renderers.*" 
	layout="vertical" backgroundAlpha="100" defaultButton="{saveButton}">

	<mx:HTTPService             
		id="saveTopic" 
		url="SaveTopic.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onSaveTopic(event)"
		fault="parentApplication.onFailHttpRequest('Failed to update topic', event)"
		method="POST"
		useProxy="false">
		<mx:request>
		</mx:request>
	</mx:HTTPService>	
	
	<mx:HTTPService             
		id="getLab" 
		url="GetLab.gx"
		concurrency="multiple"
		destination="getLab" 
		resultFormat="e4x"
		result="onGetLab(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get lab', event)"
		method="POST" 
		useProxy="false">
	</mx:HTTPService> 
       
    <mx:Script>
	<![CDATA[
	
	import mx.collections.Sort;
	import mx.collections.XMLListCollection;
	import mx.controls.Alert;
	import mx.managers.PopUpManager;
	import mx.rpc.events.ResultEvent;	
	import views.util.GNomExStringUtil;

	
	[Bindable]
	public var idParentTopic:Object = null;
	[Bindable]
	public var windowTitle:Object = null;
		
	private var idLab:String;
		
	[Bindable]
	private var appUsersForLab:XMLListCollection;
	private var appUserSort:Sort;	
		
	private var parentView:DisplayObject;
	private var parentTopic:Object;
		
	private var isAdmin:Boolean = false;

	public function init(parentTopic:Object, parentView:DisplayObject):void {
		var item:Object;
		
		this.parentView = parentView;
		this.parentTopic = parentTopic;
		
		if(parentApplication.isSuperAdmin || parentApplication.hasPermission(parentApplication.CAN_ADMINISTER_ALL_CORE_FACILITIES)) {
			isAdmin = true;
		}	
		
		if(parentTopic == null) {
			// If top level topic
			this.idParentTopic = null;
			windowTitle = "Add new top level topic";
		} else {
			// If subtopic						
			this.idParentTopic = parentTopic.@idTopic;
			windowTitle = "Add subtopic of " + parentTopic.@label;
			if(parentTopic.hasOwnProperty("@idLab") && parentTopic.@idLab != null && parentTopic.@idLab != "") {
				for each (item in labCombo.dataProvider) {
					if (item.@idLab == parentTopic.@idLab) {
						labCombo.selectedItem = item;
						break;
					}
				}				
			}
		}
		
		labSelected();	
	}
		
	private function onGetLab(event:ResultEvent):void {			
		if (getLab.lastResult.name() == "OpenLabList") {		
			var lab:Object = getLab.lastResult..Lab.(@idLab == idLab);
			var emptyNode:XML = <AppUser idAppUser='' displayName=''/>;
			appUsersForLab = new XMLListCollection(lab.members.AppUser.(@isActive=='Y'));
			for each (var mgr:Object in lab.managers.AppUser) {
				if (!appUsersForLab.contains(mgr)) {
					if(mgr.@isActive=='Y') {
						appUsersForLab.addItem(mgr);
					}
					
				}
			}
			appUsersForLab.addItemAt(emptyNode, 0);
			
			appUserSort = new Sort();
			appUserSort.compareFunction = GNomExStringUtil.sortAppUsers;
			appUsersForLab.sort = appUserSort;
			appUsersForLab.refresh();
			ownerCombo.dataProvider = appUsersForLab;
			ownerCombo.selectedItem = emptyNode;
			
			var a:Object;
			if(isAdmin && parentTopic != null) {
				// If admin and subtopic then change default owner to parent owner
				for each(a in appUsersForLab) {
					if (a.@idAppUser == parentTopic.@idAppUser) {
						ownerCombo.selectedItem = a;
						break;
					}
				}
			}	else {
				// Set default owner to current user
				for each(a in appUsersForLab) {
					if (a.@idAppUser == parentApplication.idAppUser) {
						ownerCombo.selectedItem = a;
						break;
					}
				}					
			}			
		}
	}
				

   	private function onSaveTopic(event:ResultEvent):void {
		if (saveTopic.lastResult.name() == "SUCCESS") {			
			closeWindow();
			var itemToSelect:XML = new XML(
			   "<Topic " +
			     " idLab='" + (labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : '') + "'" +
				 " idTopic='" + saveTopic.lastResult.@idTopic + "'" +
				 " codeVisibility='" + saveTopic.lastResult.@codeVisibility + "'" +
			    "/>");
			if (parentView is NavTopicsView) {
				NavTopicsView(parentView).refreshTopicsAndSelect(itemToSelect);
			}
		} else {
			Alert.show(saveTopic.lastResult..ACTMESSAGE.@TEXT);
		}	
	}

	private function save():void {
		if (labCombo.selectedItem == null) {
			labCombo.setFocus();
			Alert.show("Please select a Lab.");
			return;        		
		} 
		if (ownerCombo.selectedItem == null || ownerCombo.selectedIndex == 0) {
			ownerCombo.setFocus();
			Alert.show("Please select the topic owner.");
			return;        		
		} 
		var selectedIdLab:String = labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : "";
		var params:Object = new Object();
		params.idParentTopic      	  = idParentTopic;
		params.name                   = this.topicName.text;
		params.description            = "";
		params.idLab                  = selectedIdLab;
		params.idAppUser              = ownerCombo.selectedItem != null ? ownerCombo.selectedItem.@idAppUser : "";
		params.codeVisibility         = "MEM";
		
		saveTopic.send(params);
	}   
		
    private function closeWindow():void {
		PopUpManager.removePopUp(this);		
	}
		
	private function labSelected():void {		
		var params:Object = new Object();
		idLab = labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : "";
		if(idLab.length > 0) {
			params.idLab = idLab;
			params.includeBillingAccounts = "N";
			params.includeProductCounts = "N";
			params.includeSubmitters = "N";
			params.includeMoreCollaboratorInfo = "N";
			params.includeHistoricalOwnersAndSubmitters = "N";
			getLab.send(params);
		}
	}		

	]]>
    </mx:Script>
    <mx:VBox width="100%" height="100%" verticalGap="10">
		<mx:HBox width="100%" >
			<mx:Label text="Name" width="105"/>
			<mx:TextInput id="topicName" width="308" maxChars="2000"/>
		</mx:HBox>		
		<mx:HBox width="100%" >
			<mx:Label text="Group" width="105"/>
			<renderers:FilterComboBox dataProvider="{parentApplication.submitRequestLabList.source}" 
									  labelField="@name" 
									  prompt="Lab..."
									  width="308"
									  close="labSelected();" 
									  id="labCombo"/>
		</mx:HBox>		
		<mx:HBox width="100%" >
			<mx:Label text="Owner" width="105"/>
			<util:ComboBoxDatProvChangeFix  width="308" id="ownerCombo" labelField="@displayName" dataProvider="{appUsersForLab}"></util:ComboBoxDatProvChangeFix>						         			
		</mx:HBox>		

    </mx:VBox>

	<mx:ControlBar horizontalAlign="right" height="29">
		<mx:Button id="saveButton"  
			enabled="{(topicName.text != null &amp;&amp; topicName.text.length > 0) ? true : false}" 
			icon="{parentApplication.iconSave}" 
			disabledIcon="{parentApplication.iconSaveDisabled}" 
			label="Save"
			click="{save();}"/>
		<mx:Button id="cancelButton" label="Cancel" click="closeWindow()"/>
	</mx:ControlBar>
</mx:TitleWindow>
