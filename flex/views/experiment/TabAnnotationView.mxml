<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas label="Annotations"  xmlns:mx="http://www.adobe.com/2006/mxml"
		   xmlns:util="views.util.*" show="{this.init()}"
		   width="100%" height="100%" enabled="false">

	<mx:HTTPService
			id="saveProperty"
			url="SaveProperty.gx"
			resultFormat="e4x"
			showBusyCursor="true"
			result="onSaveProperty(event)"
			fault="parentApplication.onFailHttpRequest('Failed to save annotation', event)"
			method="POST"
			useProxy="false">
	</mx:HTTPService>

	<mx:HTTPService
			id="addPropertyAppUsers"
			url="AddPropertyAppUsers.gx"
			resultFormat="e4x"
			showBusyCursor="true"
			result="onAddPropertyAppUsers(event)"
			fault="parentApplication.onFailHttpRequest('Failed to add AppUsers to Property', event)"
			method="POST"
			useProxy="false">
	</mx:HTTPService>

	<mx:states>
		<mx:State name="EditState">
		</mx:State>
		<mx:State name="ExternalState">
			<mx:SetProperty target="{text1}" name="label" value="Check the sample annotations that apply"/>
		</mx:State>
	</mx:states>
	<mx:Script>
		<![CDATA[
		import hci.flex.controls.DropdownLabel;
		import hci.flex.controls.Label;
		import hci.flex.controls.TextInput;
		import hci.flex.dictionary.DictionaryEditor;
		import hci.flex.dictionary.DictionaryEvent;

		import mx.collections.XMLListCollection;
		import mx.controls.Alert;
		import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
		import mx.controls.dataGridClasses.DataGridColumn;
		import mx.controls.dataGridClasses.DataGridItemRenderer;
		import mx.events.ListEvent;
		import mx.managers.PopUpManager;
		import mx.rpc.events.ResultEvent;
		import mx.utils.StringUtil;

		import views.admin.PropertyEvent;
		import views.admin.PropertyWindow;
		import views.renderers.CheckBoxRenderer;
		import views.renderers.ComboBox;
		import views.renderers.MultiselectRenderer;
		import views.renderers.URLRenderer;
		import views.util.AnnotationAdvancedDataGridColumn;
		import views.util.AnnotationDataGridColumn;
		import views.util.AnnotationUtility;

		private var dictionaryEditor:DictionaryEditor;

		public var ignoreToggle:Boolean = false;

		private var idPropertyAdded:String = null;

		private function init():void {
			if (parentDocument != null) {
				parentDocument.propertyEntries.refresh();
				filteredProperties.refresh();
				updateSelectedProperties();
			}
		}

		public function checkSecurity():void {
			if (parentDocument == null) {
				return;
			}
			if (parentDocument.isEditState() && parentDocument.request.@isExternal == 'Y') {
				currentState = "ExternalState";
			} else if (parentDocument.isEditState() && parentDocument.request.@isExternal != 'Y') {
				currentState = "EditState";
			} else if (parentApplication.submitInternalExperiment()) {
				currentState = "";
			} else {
				currentState = "ExternalState";
			}

		}

		public function selectAnnotation(dataField:String):void {
			if (dataField.substr(0, 6) == '@ANNOT') {
				var idProperty:String = dataField.substr(6);
				if (parentDocument != null) {
					for each(var node:XML in parentDocument.propertyEntries) {
						if (node.@idProperty == idProperty) {
							node.@isSelected = true;
						}
					}
				}
			}
		}


		//
		// Configure properties
		//
		private function editPropertyDictionary():void {
			var configWindow:PropertyWindow = PropertyWindow(PopUpManager.createPopUp(parentApplication.theBody, PropertyWindow, true));
			configWindow.showSampleAnnotations.selected = true;
			configWindow.showAnalysisAnnotations.selected = false;
			configWindow.showDataTrackAnnotations.selected = false;
			configWindow.onPropertyRefreshed(null);
			configWindow.idCoreFacilitySelected = parentDocument.coreFacility.@idCoreFacility;
			PopUpManager.centerPopUp(configWindow);


			parentApplication.removeEventListener(PropertyEvent.DATA_REFRESHED,
					onPropertyRefreshed);
			parentApplication.addEventListener(PropertyEvent.DATA_REFRESHED,
					onPropertyRefreshed);


		}


		public function onPropertyRefreshed(event:Event):void {

			if (parentDocument == null || parentDocument.request == null) {
				parentApplication.removeEventListener(PropertyEvent.DATA_REFRESHED,
						onPropertyRefreshed);
				return;
			}
			parentDocument.propertyEntries.refresh();

			this.checkForRemovedAnnotations();
			this.checkForNewAnnotations();

			filteredProperties.source = null;
			filteredProperties.source = parentDocument.propertyEntries.source;
			filteredProperties.refresh();
			updateSelectedProperties();

			if (idPropertyAdded != null) {
				var addedProperty:Object = parentApplication.getSampleProperty(idPropertyAdded);
				selectAndScrollToName(addedProperty.@name);
				idPropertyAdded = null;
				this.enabled = true;
			}
		}


		private function checkForNewAnnotations():void {
			var sc:XML;
			var annotation:Object;
			var exists:Boolean = false;
			for each(sc in parentApplication.propertyList) {
				exists = false;

				for each(annotation in parentDocument.request.PropertyEntries.PropertyEntry) {
					if (sc.@value == annotation.@idProperty) {
						exists = true;
						if (sc.@name != annotation.@name) {
							annotation.@name = sc.@name;
						}
						break;
					}
				}
				if (!exists && sc.@forSample == 'Y') {

					var node:XML = new XML("<PropertyEntry/>");
					node.@idProperty = sc.@value;
					node.@name = sc.@display;
					node.@otherLabel = "";
					node.@isSelected = "false";

					parentDocument.propertyEntries.addItem(node);

				}
			}
			parentDocument.propertyEntries.refresh();
		}


		private function checkForRemovedAnnotations():void {
			var annotation:Object;
			var exists:Boolean = false;
			for each(annotation in parentDocument.propertyEntries) {
				exists = false;
				for each(var sc:XML in parentApplication.propertyList) {
					if (sc.@value == annotation.@idProperty || annotation.@idProperty == "-1") {
						exists = true;
						break;
					}
				}
				if (!exists) {
					// Remove the sample characteristic entry
					parentDocument.propertyEntries.removeItemAt(parentDocument.propertyEntries.getItemIndex(annotation));
				}
			}

			// Hide the existing annotation columns that don't have a corresponding sample characteristic entry
			for each(var property:Object in parentApplication.propertyList) {
				exists = false;
				for each(var sce:XML in parentDocument.propertyEntries) {
					if (sce.@value == property.@idProperty) {
						exists = true;
						break;
					}
				}

			}
		}

		public function changeOtherHeaderLabelAndToggle(data:Object):void {
			if (data.@otherLabel == '') {
				data.@isSelected = false;
			} else {
				data.@isSelected = true;
			}
			changeOtherHeaderLabel(data);

		}

		public function changeOtherHeaderLabel(data:Object):void {

			parentDocument.request.@otherLabel = data.@otherLabel;

		}

		public function createToolTip(event:ListEvent, dataGrid:DataGrid):void {
			createAnnotationList();
			var description:String = "";
			for each (var temp:XML in properties) {
				if (temp.@name == dataGrid.dataProvider[event.rowIndex].@name) {
					description = temp.@description;
					break;
				}
			}
			dataGrid.toolTip = description;
		}

		/*
		 * Returns list of users whose annotations should appear on the annotation list
		 *
		 */
		private function getAnnotationAllowedUserList():Array {
			var userList:Array = new Array();
			// Owner of the experiment
			pushUnique(userList, parentDocument.getIdAppUserOwner());

			// Submitter -- if different and not null
			pushUnique(userList, parentDocument.getIdAppUserSubmitter());

			// current user -- if different
			pushUnique(userList, parentApplication.getIdAppUser());

			return userList;
		}

		private function pushUnique(a:Array, v:String):void {
			if (v != null && v != '') {
				for each(var v1:String in a) {
					if (v1 == v) {
						return;
					}
				}
				a.push(v);
			}
		}

		private function createAnnotationList():void {
			properties = new XMLListCollection();
			for each(var sc:XML in parentApplication.propertyList) {
				properties.addItem(sc.copy());
			}
		}

		private function addNewAnnotation():void {
			if (newAnnotationName.text == null || newAnnotationName.text.length == 0 || newAnnotationName.text == PropertyWindow.NAME_FIELD_DEFAULT) {
				Alert.show('Please enter name of new annotation in the text box before pressing Add Annotation');
				return;
			}

			var found:Boolean = selectAndScrollToName(newAnnotationName.text);
			var addUser:Boolean = false;
			var existingAnnotation:Object = null;

			// Note if the annotation is visible in the screen they have to choose a different name.
			if (!found) {
				existingAnnotation = findAnnotation(newAnnotationName.text);
				if (existingAnnotation != null) {
					var orgs:XMLList = existingAnnotation.descendants("Organism");
					var platforms:XMLList = existingAnnotation.descendants("PropertyPlatformApplication");
					if (orgs.length() == 0 && platforms.length() == 0 && existingAnnotation.@forSample == 'Y') {
						// if not filtered by organism or platform or forSample then it must be filtered by user.  So we want to add in the user.
						addUser = true;
					} else {
						// If filtered by organism or platform or forSample they have to choose a different name.
						found = true;
					}
				}
			}

			if (found) {
				Alert.show('"' + newAnnotationName.text + '" has been used as the name for a previously defined annotation. Please choose another name.');
				return;
			}

			if (addUser) {
				addUserToProperty(existingAnnotation);
			} else {
				addNewProperty(newAnnotationName.text);
			}

			newAnnotationName.text = PropertyWindow.NAME_FIELD_DEFAULT;
		}

		private function addNewProperty(name:String):void {
			this.enabled = false;

			var params:Object = new Object();
			params.idProperty = '';
			params.name = name;
			params.isActive = 'Y';
			params.isRequired = 'N';
			params.forAnalysis = "N";
			params.forDataTrack = "N";
			params.forRequest = "N";
			params.forSample = "Y";
			params.idCoreFacility = parentDocument.coreFacility.@idCoreFacility;
			if (parentApplication.hasPermission("canWriteDictionaries")) {
				params.idAppUser = '';
			} else {
				params.idAppUser = parentApplication.getIdAppUser();
			}
			params.codePropertyType = 'TEXT';
			params.optionsXMLString = "<options>" + "</options>";
			params.organismsXMLString = "<organisms>" + "</organisms>";
			params.platformsXMLString = "<platformApplications>" + "</platformApplications>";
			params.analysisTypesXMLString = "<analysisTypes>" + "</analysisTypes>";

			if (parentApplication.getCoreFacilityProperty(parentDocument.coreFacility.@idCoreFacility, parentApplication.PROPERTY_RESTRICT_ANNOTATION_ON_AUTO_ADD) == "Y") {
				params.appUsersXMLString = getNewAppUsersParam();
			} else {
				params.appUsersXMLString = "<appUsers></appUsers>";
			}

			saveProperty.send(params);
		}

		private function addUserToProperty(property:Object):void {
			this.enabled = false;

			var params:Object = new Object();
			params.idProperty = property.@idProperty;

			params.appUsersXMLString = getNewAppUsersParam();

			addPropertyAppUsers.send(params);
		}

		private function getNewAppUsersParam():String {
			var appUsers:XMLList = new XMLList();
			for each (var idAppUser:Object in getAnnotationAllowedUserList()) {
				appUsers += XML("<AppUserLite idAppUser='" + idAppUser + "'/>");
			}
			return "<appUsers>" + appUsers.toXMLString() + "</appUsers>";
		}

		private function findAnnotation(name:String):Object {
			for each(var sc:Object in parentApplication.propertyList) {
				if (sc.@idCoreFacility == parentDocument.coreFacility.@idCoreFacility) {
					var thisPropName:String = StringUtil.trim(sc.@name);
					thisPropName = thisPropName.toLowerCase();
					if (name.toLowerCase() == thisPropName) {
						return sc;
					}
				}
			}
			return null;
		}

		private function onSaveProperty(event:ResultEvent):void {
			if (saveProperty.lastResult.name() == "SUCCESS") {
				this.idPropertyAdded = saveProperty.lastResult.@idProperty;
				parentApplication.removeEventListener(PropertyEvent.DATA_REFRESHED,
						onPropertyRefreshed);
				parentApplication.addEventListener(PropertyEvent.DATA_REFRESHED,
						onPropertyRefreshed);
				parentApplication.dictionaryManager.call("reload", "");
				parentApplication.getPropertyList.send();
			} else {
				this.enabled = true;
				Alert.show(saveProperty.lastResult..ACTMESSAGE.@TEXT);
			}
		}

		private function onAddPropertyAppUsers(event:ResultEvent):void {
			if (addPropertyAppUsers.lastResult.name() == "SUCCESS") {
				this.idPropertyAdded = addPropertyAppUsers.lastResult.@idProperty;
				parentApplication.removeEventListener(PropertyEvent.DATA_REFRESHED,
						onPropertyRefreshed);
				parentApplication.addEventListener(PropertyEvent.DATA_REFRESHED,
						onPropertyRefreshed);
				parentApplication.dictionaryManager.call("reload", "");
				parentApplication.getPropertyList.send();
			} else {
				this.enabled = true;
				Alert.show(addPropertyAppUsers.lastResult..ACTMESSAGE.@TEXT);
			}
		}

		private function selectAndScrollToName(name:String):Boolean {
			var found:Boolean = false;
			var idx:int = 0;
			for each (var a:Object in filteredProperties) {
				if (a.@name.toString().toLowerCase() == name.toLocaleLowerCase()) {
					found = true;
					a.@isSelected = 'true';
					sampleAnnotationGrid.scrollToIndex(idx);
					break;
				}
				idx++;
			}

			return found;
		}

		private function filterPropertiesByUser(item:Object):Boolean {
			// Get property with children (organisms, platforms, appusers).
			var property:XML = parentApplication.getSampleProperty(item.@idProperty);
			if (parentDocument == null || item.@idProperty == "-1" || property == null) {
				return true;
			}

			// Should figure some way to remove this dependency on parentDocument.
			var keep:Boolean = parentDocument.filterPropertyEntryWithFullProperty(property, item);
			if (keep) {
				keep = false;
				var users:XMLList = property.appUsers.AppUserLite;
				if (users.length() <= 0) {
					keep = true;
				} else {
					var allowedUsers:Array = getAnnotationAllowedUserList();
					for each (var u:Object in users) {
						for each (var u1:Object in allowedUsers) {
							if (u1.toString() == u.@idAppUser.toString()) {
								keep = true;
								break;
							}
						}
						if (keep) {
							break;
						}
					}
				}
			}
			return keep;
		}

		private function toggleProperty(event:ListEvent, dataGrid:DataGrid):void {
			if (event.columnIndex > 0) {
				if (dataGrid.dataProvider[event.rowIndex].@isSelected == "true") {
					dataGrid.dataProvider[event.rowIndex].@isSelected = "false";
				} else {
					dataGrid.dataProvider[event.rowIndex].@isSelected = "true";
				}
			}
		}

		private function toggleFilteredAnnotation(event:ListEvent):void {
			toggleProperty(event, sampleAnnotationGrid);
			updateSelectedProperties();
		}

		private function toggleSelectedAnnotation(event:ListEvent):void {
			toggleProperty(event, selectedAnnotationGrid);
			propagateSelectedProperty(event);
		}

		private function propagateSelectedProperty(event:ListEvent):void {
			var selectedProperty:Object = selectedAnnotationGrid.dataProvider[event.rowIndex];
			for each (var filteredProperty:Object in filteredProperties) {
				if (filteredProperty.@idProperty == selectedProperty.@idProperty) {
					filteredProperty.@isSelected = selectedProperty.@isSelected;
				}
			}
			updateSelectedProperties();
		}

		private function updateSelectedProperties():void {
			callLater(updateSelectedPropertiesDelayed);
		}

		private function updateSelectedPropertiesDelayed():void {
			selectedProperties = new XMLListCollection();
			for each (var property:Object in filteredProperties) {
				if (property.@isSelected == "true") {
					selectedProperties.addItem(property.copy());
				}
			}
		}
		]]>
	</mx:Script>

	<mx:XMLListCollection id="filteredProperties" source="{parentDocument.propertyEntries.source}" filterFunction="filterPropertiesByUser" sort="{sortFilteredProperties}"/>
	<mx:XMLListCollection id="selectedProperties" />
	<mx:Sort id="sortFilteredProperties" compareFunction="AnnotationUtility.sortProperties" unique="true"/>

	<mx:XMLListCollection id="properties"/>

	<mx:VBox width="100%" height="100%">
		<mx:HBox width="100%" height="100%">
			<mx:VBox width="340" height="100%">
				<util:ContextHelp context1="annotationTabHelp" context2="{parentDocument.coreFacility.@idCoreFacility}" showEdit="{parentApplication.isAdminState}" title="Annotation Help"
								  label="(1) Check sample characteristics" labelPlacement="left" id="text1" width="100%"/>
				<mx:DataGrid id="sampleAnnotationGrid"
							 width="100%"
							 height="100%"
							 showHeaders="false"
							 headerHeight="20"
							 rowHeight="20"
							 variableRowHeight="true"
							 dataProvider="{filteredProperties}"
							 change="updateSelectedProperties();"
							 itemClick="toggleFilteredAnnotation(event)"
							 itemRollOver="createToolTip(event, sampleAnnotationGrid)"
							 itemRollOut="sampleAnnotationGrid.toolTip = null;" >
					<mx:columns>
						<mx:DataGridColumn
								headerText=" "
								width="20"
								editable="false"
								dataField="@isSelected"
								editorDataField="selected"
								itemRenderer="views.renderers.CheckBoxIsSelectedProperty">
						</mx:DataGridColumn>
						<mx:DataGridColumn
								headerText="Sample annotations"
								editable="true"
								editorDataField="selected"
								dataField="@name" >
							<mx:itemRenderer>
								<mx:Component>
									<mx:HBox horizontalScrollPolicy="off">
										<mx:Label text="{data.@name}" fontWeight="{data.hasOwnProperty('@isRequired') &amp;&amp; data.@isRequired == 'true' ? 'bold' : 'normal'}">
										</mx:Label>
										<mx:TextInput id="scOtherLabel" fontSize="9"
													  text="{data.@otherLabel}"
													  focusIn="{parentDocument.ignoreToggle = true}"
													  focusOut="{parentDocument.ignoreToggle = false}"
													  change="{parentDocument.ignoreToggle = true;data.@otherLabel = scOtherLabel.text;parentDocument.changeOtherHeaderLabelAndToggle(data)}"
													  width="{data.@name == 'Other' ? 130 : 0}"
													  height="{data.@name == 'Other' ? 20 : 0}"
													  visible="{data.@name == 'Other' ? true : false}"
													  editable="{data.@name == 'Other' ? true : false}">
										</mx:TextInput>
									</mx:HBox>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
					</mx:columns>
				</mx:DataGrid>
			</mx:VBox>
			<mx:Spacer width="70" />
			<mx:VBox width="340" height="100%">
				<mx:Label text="Selected characteristics" />
				<mx:DataGrid id="selectedAnnotationGrid"
							 width="100%"
							 height="100%"
							 showHeaders="false"
							 headerHeight="20"
							 rowHeight="20"
							 variableRowHeight="true"
							 dataProvider="{selectedProperties}"
							 change="propagateSelectedProperty(event);"
							 itemClick="toggleSelectedAnnotation(event)"
							 itemRollOver="createToolTip(event, selectedAnnotationGrid)"
							 itemRollOut="selectedAnnotationGrid.toolTip = null;" >
					<mx:columns>
						<mx:DataGridColumn
								headerText=" "
								width="20"
								editable="false"
								dataField="@isSelected"
								editorDataField="selected"
								itemRenderer="views.renderers.CheckBoxIsSelectedProperty">
						</mx:DataGridColumn>
						<mx:DataGridColumn
								headerText="Sample annotations"
								editable="true"
								editorDataField="selected"
								dataField="@name" >
							<mx:itemRenderer>
								<mx:Component>
									<mx:HBox horizontalScrollPolicy="off">
										<mx:Label text="{data.@name}" fontWeight="{data.hasOwnProperty('@isRequired') &amp;&amp; data.@isRequired == 'true' ? 'bold' : 'normal'}" />
									</mx:HBox>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
					</mx:columns>
				</mx:DataGrid>
			</mx:VBox>
		</mx:HBox>

		<mx:HBox width="100%">
			<mx:TextInput id="newAnnotationName" text="{PropertyWindow.NAME_FIELD_DEFAULT}" width="250"
						  focusIn="{newAnnotationName.text == PropertyWindow.NAME_FIELD_DEFAULT ? newAnnotationName.text = '' : newAnnotationName.text = newAnnotationName.text}"
						  focusOut="{newAnnotationName.text == '' ? newAnnotationName.text = PropertyWindow.NAME_FIELD_DEFAULT : newAnnotationName.text = newAnnotationName.text}"/>
			<mx:Button id="newAnnotationNameButton" click="addNewAnnotation()" label="Add Annotation"/>
			<util:ContextHelp context1="annotationTabAddHelp" context2="{parentDocument.coreFacility.@idCoreFacility}" showEdit="{parentApplication.isAdminState}" title="Add Annotation Help"
							  label="" labelPlacement="left" id="annotationTabAddHelp"/>
			<mx:Spacer width="40"/>
			<mx:LinkButton label="Edit annotations ..." textDecoration="underline" styleName="blueLinkButton" click="editPropertyDictionary()" id="editSampleAnnotationButton">
			</mx:LinkButton>
		</mx:HBox>

	</mx:VBox>

</mx:Canvas>
