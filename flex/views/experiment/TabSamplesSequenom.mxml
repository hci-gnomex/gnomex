<?xml version="1.0" encoding="utf-8"?>
<exp:TabSamplesBase show="{this.init();}"
					label="Samples" width="100%" height="100%"  enabled="false" 
				    xmlns:mx="http://www.adobe.com/2006/mxml" 
				    xmlns:util="views.util.*"
				    xmlns:exp="views.experiment.*"
				    xmlns:renderers="views.renderers.*" 
				    xmlns:grid="views.util.grid.*" >

	<mx:Script>
		<![CDATA[
			import hci.flex.controls.DropdownLabel;
			import hci.flex.controls.Label;
			
			import mx.collections.IHierarchicalCollectionView;
			import mx.collections.SortField;
			import mx.collections.XMLListCollection;
			import mx.controls.AdvancedDataGrid;
			import mx.controls.Alert;
			import mx.controls.TextInput;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.events.ValidationResultEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			import mx.utils.ObjectUtil;
			import mx.utils.StringUtil;
			import mx.validators.NumberValidator;
			import mx.validators.ValidationResult;
			
			import views.renderers.CheckBoxRenderer;
			import views.renderers.ComboBox;
			import views.renderers.ComboBoxWorkflowStatus;
			import views.renderers.GridColumnFillButton;
			import views.renderers.MultiselectRenderer;
			import views.renderers.TextInputSeqPrepByCore;
			import views.renderers.URLRenderer;
			import views.util.AdvancedDataGridColumnWithType;
			import views.util.AnnotationAdvancedDataGridColumn;
			import views.util.CopySampleSheetColumnView;
			import views.util.SampleConcentrationValidator;
			import views.util.UploadSampleSheetInstructionsView;
			import views.util.UploadSampleSheetView;
			import views.util.SampleQCValidator;
			
			private var saveInstructionToolTipText:String;
						
			[Bindable]
			private var isExternal:Boolean = false;
			
			private var uploadFR:FileReference;
			private var uploadSampleSheetURL:String;
			private var docTypes:FileFilter;
			private var hasColumnNames:Boolean; 
			private var	sampleSheetXML:XML;
			
			override public function getSamplesGrid():AdvancedDataGrid {
				return this.samplesGrid;
			}
			
			override public function getCCNumberBox():HBox {
				return this.ccNumberContainer;
			}

			override public function getShowCCNumberCheckbox():CheckBox {
				return this.showCCNumberCheckbox;
			}

			override public function getButtonsContainer():HBox {
				return this.buttonsContainer;
			}
			
			override public function getAddOrganismContainer():HBox {
				return this.addOrganismContainer;
			}
			
			override protected function getNumPlates():int {
				var numPlates:int = 0;
				if (hasPlates()) {
					numPlates = parentDocument.numIScanPlates;
				}
				return numPlates;
			}
			
			override protected function getNextPlate():int {
				var nextPlate:int = 0;
				if (hasPlates()) {
					for each (var sample:Object in parentDocument.samples) {
						var thisPlate:int = 0;
						if (sample.@name != '') {
							if (sample.@plateName == parentDocument.sampleSetupView.getSequenomPlateName(1)) thisPlate = 1;
							if (sample.@plateName == parentDocument.sampleSetupView.getSequenomPlateName(2)) thisPlate = 2;
							if (sample.@plateName == parentDocument.sampleSetupView.getSequenomPlateName(3)) thisPlate = 3;
							if (sample.@plateName == parentDocument.sampleSetupView.getSequenomPlateName(4)) thisPlate = 4;
						}
						if (thisPlate > nextPlate) {
							nextPlate = thisPlate;
						}
					}
					if (nextPlate < 4) {
						nextPlate = nextPlate + 1;
					} else {
						nextPlate = 0;
					}
				}
				return nextPlate;
			}
			
			override protected function getAddAllAnnotations():Boolean {
				return false;
			}

			override public function insertSample():Boolean {
				var emptyNode:XML = null;
				var isValid:Boolean = true;
				var index:int = samplesGrid.selectedIndex;
				
				emptyNode = this.getEmptySequenomNode(index);
				
				if (emptyNode != null) {
					parentDocument.samples.addItemAt(emptyNode, index);
				}
				
				if ( parentDocument.samples.length > 96 && parentDocument.samples.length % 96 != 0 ) {
					parentDocument.samples.removeItemAt(parentDocument.samples.length-1);
				}
				
				samplesGrid.updateWellNames();
				
				return isValid;
			}
			
			override public function addSample():Boolean {
				var emptyNode:XML = null;
				var isValid:Boolean = true;
				
				emptyNode = this.getEmptySequenomNode(parentDocument.samples.length);
				
				if (emptyNode != null) {
					parentDocument.samples.addItem(emptyNode);
				}
				
				return isValid;
			}
			
			
			private function getEmptySequenomNode(idx:int):XML {
				var plateName:String = getPlateName(idx);
				var wellName:String = getWellName(idx);
				var idPlate:String = getIdPlate(idx);
				var sampleId:String = "Sample" + idx;
				var emptyNode:XML = new XML("<Sample " +
					" idSample='" + sampleId + "'" +
					" name='' description='' " +
					" concentration=''" +
					" containerType='" + (parentDocument.request.@containerType == 'PLATE' ? 'Plate' : 'Tube') + "'" +
					" canChangeSampleName='Y'" +	
					" canChangeSampleConcentration='Y'" +
					" plateName='" + (parentDocument.request.@containerType == 'PLATE' ? plateName : '') + "'" +
					" idPlate='" + (parentDocument.request.@containerType == 'PLATE' ? idPlate : '') + "'" +
					" wellName='" + (parentDocument.request.@containerType == 'PLATE' ? wellName : '') + "'" +
					" idSampleType='" + (parentApplication.submitInternalExperiment() && !this.isEditState ? parentDocument.sampleSetupView.sampleTypeRadioGroup.selectedValue : '')  + "'" +
					"/>");
				return emptyNode;
			}
						
			override public function getPlateName(idx:int):String {
				
				var plateName:String = "";
				var y:int = idx / 96;
				if ( this.isEditState && parentDocument.samples.getItemAt( y*96 ) != null ) {
					plateName = parentDocument.samples.getItemAt( y*96 ).@plateName;
				} else {
					y = y + 1;
					plateName = parentDocument.sampleSetupView.getSequenomPlateName(y);
				}
				return plateName;
			}
			
			override public function getIdPlate(idx:int):String {
				var idPlate:String = "";
				
				var y:int = idx / 96;
				if ( this.isEditState && parentDocument.samples.getItemAt( y*96 ) != null ) {
					idPlate = parentDocument.samples.getItemAt( y*96 ).@idPlate;
				} 
				
				return idPlate;
			}
			
			override protected function deleteSingleSample(sample:Object):Boolean {
				parentDocument.samples.removeItemAt(parentDocument.samples.getItemIndex(sample));
				
				if ( this.hasPlates() ){
					
					if ( parentDocument.samples.length < 96 || parentDocument.samples.length % 96 != 0 ) {
						
						parentDocument.samples.addItem(getEmptySequenomNode(95));
					}
					
					samplesGrid.updateWellNames();
					
				}
				return true;
			}
			
			
			private function deleteTheSample():void {
				var idx:int = samplesGrid.selectedIndex;
				deleteSample();
				checkSamplesCompleteness();
				samplesGrid.selectedIndex = idx;
			}
			
			override public function initializeSamplesGrid():void {
				if (parentDocument.requestToClone != null) {
					return;
				}
				
				var numberOfSamples:Number = 0;
				var numberOfSamplesToAdd:Number = 0;
				
				if (parentApplication.submitInternalExperiment() ){
					
					if (parentDocument.isEditState()) {
						numberOfSamples = parentDocument.samples.length > 0 ? new Number(parentDocument.samples.length) : 1;
					} else {
						if (!parentDocument.sampleSetupView.checkSampleSetupCompleteness()) {
							return;
						}
						if ( hasPlates() ) {
							populateSampleList();
							return;
						}
						numberOfSamples = new Number(parentDocument.sampleSetupView.numberOfSamples.text);
					}
					
				} else {
					numberOfSamples = 1;
				}
				
				if (parentDocument.samples.length <= numberOfSamples) {
					numberOfSamplesToAdd = numberOfSamples - parentDocument.samples.length;
					for(var x:int = 0; x < numberOfSamplesToAdd; x++) {
						addSample();
					}
				} else {
					while (parentDocument.samples.length > numberOfSamples) {
						parentDocument.samples.removeItemAt(parentDocument.samples.length - 1);
					}
				}
			}
			
									
			override public function hasPlates():Boolean {
				return parentDocument.request.@containerType == 'PLATE' ;
			}
						
			public function populateSampleList():void {
				var numberOfSamples:int = parentDocument.numIScanPlates*96;
				
				for (var sampleIdx:int = 0; sampleIdx < numberOfSamples; sampleIdx++) {
					
					var emptySample:Object = this.getEmptySequenomNode(sampleIdx);
					var existingSamples:XMLList = parentDocument.samples.source.(@plateName==emptySample.@plateName && @wellName==emptySample.@wellName);
					
					if ( existingSamples== null || existingSamples.length() == 0 ) {
						// If no sample in that plate and well, add empty one
						parentDocument.samples.addItemAt(emptySample, sampleIdx);
					} else if ( existingSamples.length > 1 ) {
						// If somehow we have more than one sample in the same well, we remove the extras
						for ( var ind:int = 1; ind < existingSamples.length; ind ++ ) {
							parentDocument.samples.removeItemAt(parentDocument.samples.getItemIndex(existingSamples[ind]));
						}
					}
				} 
				
				// Remove extra samples
				while (parentDocument.samples.length > numberOfSamples) {
					parentDocument.samples.removeItemAt(parentDocument.samples.length - 1);
				}
			}
			
			override protected function onPromptToClearAllSamples(event:CloseEvent):void {
				if (event.detail==Alert.YES) {
					if ( parentDocument.isEditState() ) {
						while (parentDocument.samples.length > 1) {
							parentDocument.samples.removeItemAt(parentDocument.samples.length - 1);
						}
						for each (var sample:Object in parentDocument.samples) {
							
							for each (var attribute:Object in sample.attributes()) {
								var aName:String = attribute.name();
								if ( aName != 'plateName' && aName != 'wellName' && aName != 'containerType') {
									sample["@" + attribute.name()] = '';
								}
							}
							sample.@canChangeSampleName = 'Y';
						}						
					} else {
						parentDocument.samples.removeAll();
						this.initializeSamplesGrid();
					}
						
					checkSamplesCompleteness();
					this.initializeBarcoding();
				}
			}		
			
			override public function checkSamplesCompleteness():void {
				//For some reason this code is still being called even when the samples view is not visible
				if(this.visible == false){
					return;
				}
				parentDocument.userWarningsSamples = "";
				
				var completeCount:int = 0;
				var nameCompleteCount:int = 0;
				for each(var sample:Object in parentDocument.samples) {
					if (isEntered(sample, "@name") && isEntered(sample, "@concentration")) {
						nameCompleteCount++;
					}
					completeCount++;
				}
				
				var isComplete:Boolean = false;
				if (parentDocument.samples.length > 0 && completeCount == parentDocument.samples.length) {
					
						if (nameCompleteCount >= new Number(parentDocument.sampleSetupView.numberOfSamples.text)) {
							isComplete = true;
						} else {
							isComplete = false;
						}
					
				} else if (parentDocument.isEditState()) {
					isComplete = true;
				}

				// Make sure that sample concentration is valid
				var cw:String = SampleConcentrationValidator.instance.validateSampleConcentration(parentDocument.samples);
				if (cw != "") {
					parentDocument.userWarningsSamples += cw;
					isComplete = false;
				}
				
				// Make sure that sample QC is valid
				var qcWarning:String = SampleQCValidator.instance.validateSampleQC(parentDocument.samples);
				if (qcWarning != "") {
					parentDocument.userWarningsSamples += qcWarning;
					isComplete = false;
				}

				parentDocument.confirmView.enabled = isComplete;
				parentDocument.enableNextButtons();
								
				callLater(postSamplesCompleteness, [isComplete]);
			
			}
			
			private function postSamplesCompleteness(isComplete:Boolean):void {
				
				if (!isComplete) {
					
					if (parentDocument.theTab.contains(parentDocument.visibilityView)) {
						parentDocument.visibilityView.enabled = false;					
					}
					if (parentDocument.theTab.contains(parentDocument.confirmView)) {
						parentDocument.confirmView.enabled = false;					
					}
				} 
			
			}
			
			override public function propagateContainerType(containerType:String):void {
				samplesGrid.dataProvider = null;
				var idx:int = 0;
				parentDocument.samples.disableAutoUpdate();
				for each (var sample:Object in parentDocument.samples) {
					sample.@containerType = containerType;
					if (containerType == 'Plate') {
						sample.@plateName = this.getPlateName(idx) ;
						sample.@wellName = this.getWellName(idx);
					} else {
						sample.@plateName = '';
						sample.@wellName = '';
					}
					idx++;
				}
				parentDocument.samples.enableAutoUpdate();
			}
	
			
			override public function initializeBarcoding():void {
				setSamplesGridDataProvider();
								
				// handle the download sample sheet events
				this.samplesGrid.validateNow();
				callLater(this.checkSamplesCompleteness);
			
			}

			private function setSamplesGridDataProvider():void {
				this.samplesGrid.validateNow(); // Make sure columns on samples grid are created
				
				this.samplesGrid.dataProvider = parentDocument.samples;
				
				this.samplesGrid.sortableColumns = false;
				this.samplesGrid.dragMoveEnabled = false;
				this.samplesGrid.dropEnabled = false;
				this.samplesGrid.dragEnabled = false;
				
				this.plateNameCol.visible = hasPlates();
				
			}
			
			
			
			private function plateSampleCompareFunction(a:XML, b:XML):int
			{
				if (a.hasOwnProperty("@plateName") && a.hasOwnProperty("@wellName") && b.hasOwnProperty("@plateName") && b.hasOwnProperty("@wellName")) {
					var compVal:int = 0;
					if (a.@plateName == b.@plateName) {
						compVal = compVal = ObjectUtil.numericCompare(a.@wellName.toString().substr(1), b.@wellName.toString().substr(1));
						if (compVal == 0) {
							compVal = ObjectUtil.stringCompare(a.@wellName.toString(), b.@wellName.toString());
						}
					} else {
						compVal = ObjectUtil.stringCompare(a.@plateName.toString(), b.@plateName.toString());
					}
					return compVal;
				} else {
					var aPersistFlag:Number = 0;
					var aPosition:Number = 0;
					if (a.@idSample.toString().indexOf("Sample") > -1) {
						aPosition = a.@idSample.toString().substr(6);
						aPersistFlag = 1;
					} else {
						aPosition = a.@idSample;
					}
					
					var bPersistFlag:Number = 0;
					var bPosition:Number = 0;
					if (b.@idSample.toString().indexOf("Sample") > -1) {
						bPosition = b.@idSample.toString().substr(6);
						bPersistFlag = 1; // non-persistent samples sort after the persistent ones
					} else {
						bPosition = b.@idSample;
					}
					
					if (aPersistFlag == bPersistFlag) {
						return ObjectUtil.numericCompare(aPosition, bPosition);				
					} else {
						return ObjectUtil.numericCompare(aPersistFlag, bPersistFlag);
					}
				}
			}
			
			override public function showHideColumns():void {
				isExternal = (parentDocument.isEditState() && parentDocument.request.@isExternal == 'Y') || (!parentDocument.isEditState() && !parentApplication.isInternalExperimentSubmission);
				isEditState = parentDocument.isEditState();
				
				showHideCCNumberBox();
				setButtonVisibility(true);
				samplesGrid.rowOperationsAllowed = true;
				
				// Don't allow sample editing on edit unless user is admin or experiment is external or experiment is non-submitted dna sq experiment.
				if (isEditState && !parentApplication.isAdminState && !isExternal) {
					setButtonVisibility(false);
					samplesGrid.rowOperationsAllowed = false;
				}
				
				samplesGrid.addRowEnabled = !this.hasPlates() || parentDocument.isEditState();
				samplesGrid.insertRowEnabled = this.hasPlates();
				samplesGrid.showRowNumberCol = !this.hasPlates();
				samplesGrid.hasPlates = this.hasPlates();
				samplesGrid.colorRowsByField = this.hasPlates() ? 'plateName' : null;
				
				setAddOrganismVisibility(false);
								
				// Edit columns shown
				wellNameCol.visible = hasPlates() ? true : false;
												
				sampleNumberCol.visible = parentDocument.isEditState();
				
				this.sampleTypeCol.visible = true;		
				this.concentrationCol.visible = true;
				this.qc_260_280Column.visible = true;
				
				if ( parentDocument.isClinicalSequenomState() ) {
					this.sampleNameCol.visible = false;
				} else {
					this.sampleNameCol.visible = true;
					showCCNumberCheckbox.selected = false;
					showCCNumberCheckbox.enabled = true;
				}
				
				if (!parentDocument.isEditState()) {
					if (parentDocument.theTab.contains(parentDocument.confirmView)) {
						parentDocument.confirmView.showHideColumns();									
					}
				}
				
			}
			
			override protected function rebuildSamplesGrid():void {
				var grid:AdvancedDataGrid = getSamplesGrid();
				var columns:Array = grid.columns;
				var newColumns:Array = new Array();
				if (grid != null) {
					var found:Boolean = false;
					for (var x:Number = 0; x < columns.length; x++) {
						var dc:AdvancedDataGridColumn = grid.columns[x];
						
						if (dc.dataField == null || dc.dataField.substr(0, 6) != "@ANNOT") {
							newColumns.push(dc);
						}
					}
					addAnnotationProperties(columns, newColumns);
				}
				grid.columns = newColumns;
				grid.validateNow();
			}

			public function textChange(item:String):void {
				var txt:String = item;
				if(txt.length == 30) {
					Alert.show("Maximum allowed limit is 30 characters");					
				}
			}
			
			public function changeConcentrationUnit(selectedUnit:String):void{
				for each(var s:XML in samplesGrid.dataProvider) {
					s.@codeConcentrationUnit = selectedUnit;
					
				}
			}
			

		]]>
	</mx:Script>	
	
	<mx:VBox width="100%" height="100%" verticalGap="2" paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4">
		
		<mx:HBox width="100%" horizontalGap="0" verticalAlign="middle">
			<mx:HBox verticalAlign="middle" 
					 horizontalGap="0"
					 visible="{parentApplication.isInternalExperimentSubmission &amp;&amp; samplesTabHelp.visible}" 
					 includeInLayout="{parentApplication.isInternalExperimentSubmission &amp;&amp; samplesTabHelp.visible}"  
					 id="instructionVBox">
				<util:ContextHelp context1="samplesTabHelp" 
								  context2="{isExternal? 'External' : parentDocument.coreFacility.@idCoreFacility}" 
								  context3="{(parentDocument.requestType == 'MISEQ') ? 'HISEQ' : parentDocument.requestType}"
								  showEdit="{parentApplication.isAdminState}" 
								  title="Samples Tab Instructions" label="Instructions" id="samplesTabHelp" hideLabelIfNoHelp="true"/>
				
				<mx:Spacer width="6"/>
				<mx:VRule height="20"  />
				<mx:Spacer width="6"/>
			</mx:HBox>
			
			<mx:HBox id="buttonsContainer" horizontalGap="0" verticalAlign="middle">
				<util:ContextHelp context1="addSampleHelp" context2="" context3="" 
								  showEdit="{parentApplication.isSuperAdmin}" 
								  title="Add Samples Help" label="Adding samples" id="addSampleHelp" />
				<util:ContextHelp context1="samplesGridHelp" context2="" context3=""
								  showEdit="{parentApplication.isSuperAdmin}" 
								  title="Sample Grid Help" label="Copy and paste help" id="copyPasteHelp" />
				
				<mx:Spacer width="6"/>
				<mx:VRule height="20"/>
				<mx:Spacer width="6"/>
				
				<mx:LinkButton textDecoration="underline"  horizontalGap="0" paddingLeft="0" paddingRight="0"
							   label="Upload sample sheet" 
							   click="uploadSampleSheet();" icon="@Embed(source='../../assets/upload.png')" 
							   id="uploadSampleSheetButton" toolTip="Upload sample sheet&#13;(tab delimited txt file)" />
				<mx:LinkButton textDecoration="underline" horizontalGap="0" id="uploadHintLink"  paddingRight="0" paddingLeft="0"
							   icon="@Embed(source='../../assets/information.png')" 
							   label=""  
							   click="PopUpManager.centerPopUp(PopUpManager.createPopUp(parentApplication.theBody, UploadSampleSheetInstructionsView, true));"
							   toolTip=" 1.  Download the sample sheet (see instructions) or create your own table&#13;       in a program like Excel.&#13; 2.  Fill in the table for all the samples.&#13; 3.  Save the file as a tab-delimited txt file.&#13; 4.  Click 'Upload sample sheet' and select the file you created."/>
			</mx:HBox>
			
			<mx:LinkButton textDecoration="underline"  horizontalGap="0"  paddingRight="0"
						   id="downloadButton" label="Download sample sheet" 
						   click="{downloadSampleSheet()}"   icon="@Embed(source='../../assets/download.png')"/>
			
			
			<mx:HBox id="ccNumberContainer" horizontalGap="0" verticalAlign="middle">
				<mx:Spacer width="6"/>
				<mx:VRule height="20"/>
				<mx:Spacer width="6"/>
				<mx:CheckBox id="showCCNumberCheckbox"
							 selected="false"
							 click="toggleShowCCNumber()"  
							 visible="{parentApplication.isInternalExperimentSubmission &amp;&amp; parentApplication.isBSTLinkageSupported}"
							 includeInLayout="{parentApplication.isInternalExperimentSubmission &amp;&amp; parentApplication.isBSTLinkageSupported}" 
							 toolTip="Link samples to itBioPath using CC Number"
							 label="Enter CC Numbers" 
							 fontSize="11"/>
				<util:ContextHelp context1="ccNumberCheckBoxHelp" context2="{parentDocument.coreFacility.@idCoreFacility}" showEdit="{parentApplication.isAdminState}" title="CC Number Help"
								  label="" labelPlacement="left" id="ccNumberCheckBoxHelp" hideLabelIfNoHelp="true"/>
				
			</mx:HBox>
			
			<mx:HBox id="addOrganismContainer" horizontalGap="0" verticalAlign="middle">
				<mx:Spacer width="6" />
				<mx:VRule height="20"/>
				<mx:Spacer width="6" />
				<mx:LinkButton  horizontalGap="0" label="Add Organism ..." textDecoration="underline"  
								click="addOrganism()" id="addOrganismButton"/>
				<util:ContextHelp context1="samplesTabNewOrganismHelp" 
								  context2="{parentDocument.coreFacility.@idCoreFacility}" 
								  showEdit="{parentApplication.isAdminState}" title="New Organism Help"
								  label="" labelPlacement="left" id="samplesTabNewOrganismHelp"/>
			</mx:HBox>
		</mx:HBox>

		<grid:CopyPasteSampleGrid width="100%" height="100%" id="samplesGrid"  
								  editable="true" itemEditEnd="validateData(event)"
								  itemFocusOut="checkSamplesCompleteness()" 
								  deleteRowFunction="deleteTheSample"
								  addRowEnabled="{!this.hasPlates()||parentDocument.isEditState()}"
								  insertRowFunction="insertSample"
								  addRowFunction="addSample"
								  clearAllFunction="promptToClearAllSamples"
								  showRowNumberCol="{!this.hasPlates()}"
								  hasPlates="{this.hasPlates()}">
			<grid:columns>
				<mx:AdvancedDataGridColumn id="plateNameCol" headerText="Plate" dataField="@plateName" editable="false" visible="true" width="60" >
					<mx:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0" horizontalScrollPolicy="off"  verticalScrollPolicy="off"  horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="samplePlateName" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="Plate Help" 
												  showEdit="{parentApplication.isAdminState}" label="Plate" noIconSpaceIfNoHelp="true"/>
							</mx:VBox> 
						</mx:Component>
					</mx:headerRenderer>
				</mx:AdvancedDataGridColumn>
				<mx:AdvancedDataGridColumn id="wellNameCol" visible="false" editable="false" headerText="Well"  width="50" resizable="false" dataField="@wellName">
					<mx:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0" horizontalScrollPolicy="off"  verticalScrollPolicy="off"  horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="sampleWellName" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="Well Help" 
												  showEdit="{parentApplication.isAdminState}" label="Well" noIconSpaceIfNoHelp="true"/>
							</mx:VBox> 
						</mx:Component>
					</mx:headerRenderer>
				</mx:AdvancedDataGridColumn>
				<mx:AdvancedDataGridColumn id="sampleNumberCol" headerText="ID" width="80" visible="true"  dataField="@number"  editable="false">
					<mx:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0" horizontalScrollPolicy="off"  verticalScrollPolicy="off"  horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="sampleNumber" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="Sample ID Help" 
												  showEdit="{parentApplication.isAdminState}" label="ID" noIconSpaceIfNoHelp="true"/>
							</mx:VBox> 
						</mx:Component>
					</mx:headerRenderer>
				</mx:AdvancedDataGridColumn>
				<mx:AdvancedDataGridColumn headerText="Sample Name" width="100" editorDataField="text" dataField="@name" rendererIsEditor="false"
										   id="sampleNameCol"
										   editable="true" showDataTips="true" dataTipField="Please enter maximum of 30 Characters"
										   itemRenderer="{hci.flex.controls.Label.create('@name')}" >
					<mx:itemEditor>
						<mx:Component id ="sampleNameTextField">
							<mx:TextInput  maxChars="30" change="outerDocument.textChange(this.text)"/>
						</mx:Component>
					</mx:itemEditor> 
					<mx:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0" horizontalScrollPolicy="off"  verticalScrollPolicy="off"  horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="sampleName" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="Sample Name Help" 
												  showEdit="{parentApplication.isAdminState}" label="Sample&#13;Name" noIconSpaceIfNoHelp="true"/>
							</mx:VBox> 
						</mx:Component>
					</mx:headerRenderer>
				</mx:AdvancedDataGridColumn>  

				<mx:AdvancedDataGridColumn headerText="{parentApplication.getProperty(parentApplication.PROPERTY_CUSTOM_SAMPLE_CONC_UNITS) == 'N' ? 'Conc. (ng/ul)' : 'Conc.'}" id="concentrationCol" 
										   visible="{parentApplication.isInternalExperimentSubmission}"  width="72" formatter="{sampleConcentrationFormatter}" rendererIsEditor="false"
										   itemRenderer="{hci.flex.controls.Label.create('@concentration')}"                 
										   itemEditor="{views.renderers.GridColumnFillButton.create(new views.renderers.TextInputSampleConcentration(), '')}" editorDataField="value" dataField="@concentration">
					<mx:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0" horizontalScrollPolicy="off"  verticalScrollPolicy="off"  horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="sampleConcentration" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="Concentration Help" 
												  showEdit="{parentApplication.isAdminState}" label="{outerDocument.parentApplication.getProperty(outerDocument.parentApplication.PROPERTY_CUSTOM_SAMPLE_CONC_UNITS) == 'N' ? 'Conc. (ng/ul)' : 'Conc.'}" noIconSpaceIfNoHelp="true"/>
								<mx:ComboBox id="concentrationCombo" visible="{outerDocument.parentApplication.getProperty(outerDocument.parentApplication.PROPERTY_CUSTOM_SAMPLE_CONC_UNITS) == 'Y'}" 
											 includeInLayout="{outerDocument.parentApplication.getProperty(outerDocument.parentApplication.PROPERTY_CUSTOM_SAMPLE_CONC_UNITS) == 'Y'}" dataProvider="{outerDocument.parentApplication.dictionaryManager.getEntriesExcludeBlank('hci.gnomex.model.ConcentrationUnit')}" 
											 labelField="@display" change="{outerDocument.changeConcentrationUnit(concentrationCombo.selectedLabel)}"/>
							</mx:VBox> 
						</mx:Component>
					</mx:headerRenderer>
				</mx:AdvancedDataGridColumn>
				<mx:AdvancedDataGridColumn id="ccNumber" headerText="CC Number" dataField="@ccNumber" editable="true" 
										   visible="{showCCNumberCheckbox.visible &amp;&amp; showCCNumberCheckbox.selected ? true : false}"
										   width="76"
										   itemRenderer="{hci.flex.controls.Label.createCustom('@ccNumber', parentApplication.annotationColor, parentApplication.annotationColorBorder, 0)}"                
										   itemEditor="{views.renderers.GridColumnFillButton.create(hci.flex.controls.TextInput.create('@ccNumber').newInstance(), '')}" editorDataField="value">
					<mx:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0" horizontalGap="0" horizontalScrollPolicy="off"  verticalScrollPolicy="off"  horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="sampleCCNumber" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="CC Number Help" 
												  showEdit="{parentApplication.isAdminState}" label="CC           &#13;Number" noIconSpaceIfNoHelp="true"/>
							</mx:VBox> 
						</mx:Component>
					</mx:headerRenderer>
				</mx:AdvancedDataGridColumn>
				
				<util:AdvancedDataGridColumnWithType propertyType="OPTION" id="sampleTypeCol"  headerText="Sample Type" visible="true" width="180"   dataField="@idSampleType" 
													 editorDataField="value" sortable="false" editable="true" rendererIsEditor="false" 
													 itemRenderer="{views.renderers.DropdownLabel.create(parentApplication.dictionaryManager.xml.Dictionary.(@className=='hci.gnomex.model.SampleType').DictionaryEntry, '@display', '@value', '@idSampleType', '', false, true)}"                 
													 itemEditor="{views.renderers.GridColumnFillButton.create(hci.flex.controls.ComboBox.getFactory(this.filteredSampleTypeList.source, '@display', '@value', '@idSampleType', true).newInstance(), '')}">
					<util:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0"   horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="sampleType" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="Sample type Help" showEdit="{parentApplication.isAdminState}"
												  label="Sample Type" noIconSpaceIfNoHelp="true"/>
							</mx:VBox> 
						</mx:Component>
					</util:headerRenderer>
				</util:AdvancedDataGridColumnWithType>
				
				<mx:AdvancedDataGridColumn id="qc_260_280Column"
										   editable="{parentApplication.hasPermission('canWriteAnyObject')}" 
										   headerText="QC 260/280" width="90" 
										   dataField="@qual260nmTo280nmRatio" 
										   itemEditor="{views.renderers.GridColumnFillButton.create(new mx.controls.TextInput(), '')}" 
										   editorDataField="value">
					<mx:headerRenderer > 
						<mx:Component>
							<mx:VBox verticalGap="0" horizontalScrollPolicy="off"  verticalScrollPolicy="off"  horizontalAlign="center" verticalAlign="middle"> 
								<util:ContextHelp context1="sampleQc260280" context2="{outerDocument.parentDocument.coreFacility.@idCoreFacility}" title="QC 260/280 Help" 
												  showEdit="{parentApplication.isAdminState}" label="QC 260/280" noIconSpaceIfNoHelp="true"/>
							</mx:VBox> 
						</mx:Component>
					</mx:headerRenderer>
				</mx:AdvancedDataGridColumn>
				
				
				
			</grid:columns>
		</grid:CopyPasteSampleGrid>

	</mx:VBox>
	
	<exp:states>
		<mx:State name="ClinicalSequenomState" >
			<mx:SetProperty target="{showCCNumberCheckbox}" name="selected" value="true"/>
			<mx:SetProperty target="{showCCNumberCheckbox}" name="enabled" value="false"/>
		</mx:State>
	</exp:states>

</exp:TabSamplesBase>
