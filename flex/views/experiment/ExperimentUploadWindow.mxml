<?xml version="1.0" encoding="utf-8"?>
<util:ResizableTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
						   layout="vertical" width="1000" 
						   xmlns:util="views.util.*"
						   height="408" creationPolicy="all"
						   showCloseButton="true" creationComplete="centerMe()"
						   close="{closeMe()}" 
						   title="{'Upload Files for Experiment ' + requestNumber}"  xmlns:controls="hci.flex.controls.*">
	
	<mx:HTTPService  
		id="getExperimentUploadURL" 
		url="UploadExperimentURLServlet.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onGetExperimentUploadURL(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to get experiment upload URL', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	
	<mx:HTTPService  
		id="fdtUploadStart" 
		url="FastDataTransferUploadStart.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onFdtUploadStart(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to start FDT upload', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	
	
	<mx:HTTPService  
		id="getRequest" 
		url="GetRequest.gx"
		resultFormat="e4x"
		showBusyCursor="true" 
		result="onGetRequest(event)" 
		fault="parentApplication.onFailHttpRequest('Failed to get experiment', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService>
	
	<mx:HTTPService  
		id="getRequestDownloadList" 
		url="GetRequestDownloadList.gx"
		destination="getRequestDownloadList"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onGetRequestDownloadList(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get request download list', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:HTTPService  
		id="organizeExperimentUploadFiles" 
		url="OrganizeExperimentUploadFiles.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onOrganizeExperimentUploadFiles(event)"
		fault="parentApplication.onFailHttpRequest('Failed to organize experiment upload files', event)"
		method="POST"
		useProxy="false">
	</mx:HTTPService >
	
	<mx:HTTPService  
		id="getLinkedSampleFiles" 
		url="GetLinkedSampleFiles.gx"
		resultFormat="e4x"
		showBusyCursor="true"
		result="onGetLinkedSampleFiles(event)"
		fault="parentApplication.onFailHttpRequest('Failed to link sample files', event)"
		method="POST"
		useProxy="false">
		<mx:request>
			<idRequest>{this.idRequest}</idRequest>
		</mx:request>
	</mx:HTTPService >
	
	<mx:Script>
		<![CDATA[
			import mx.collections.HierarchicalData;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.controls.Alert;
			import mx.controls.List;
			import mx.controls.listClasses.ListBase;
			import mx.core.DragSource;
			import mx.events.CloseEvent;
			import mx.events.DragEvent;
			import mx.events.ListEvent;
			import mx.managers.DragManager;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			
			import views.util.MultiFileUpload;
			
			[Bindable]
			public var idRequest:Object;
			
			[Bindable]
			private var requestNumber:Object;
			
			[Bindable]
			private var request:Object;
			
			[Bindable]
			public var parentView:ExperimentEditView = new ExperimentEditView;
			
			[Bindable]
			private var sampleList:XMLListCollection = new XMLListCollection();
			
			[Bindable]
			private var experimentFileList:XMLListCollection = new XMLListCollection();
			
			[Bindable]
			private var selectedSampTreeItem:Object = new Object();
			
			private var multiFileUpload:MultiFileUpload;
			private var expFileDict:Dictionary = new Dictionary();
			
			private var downloadDraggedItems:Array = null;
			
			private var sampleTreeDraggedItems:Array = null;
			
			public function init(request:Object):void {
				this.idRequest = request.@idRequest;
				this.requestNumber = request.@requestNumber;
				this.request = request;
				
				//remove linkSamplesTab if property not enabled and if the experiment isn't hiseq or miseq
				if(parentApplication.getProperty(parentApplication.PROPERTY_EXPERIMENT_FILE_SAMPLE_LINKING_ENABLED) == 'N' ||
					(request.@codeRequestCategory != parentApplication.TYPE_MISEQ &&  request.@codeRequestCategory != parentApplication.TYPE_HISEQ)){
					theTab.removeChildAt(2);
				}
				
				fdtStartButton.enabled = true;
				saveUUID.text = "";
				
				getExperimentUploadURL.send();
				
				var params:Object = new Object();
				params.idRequest = idRequest;
				params.includeUploadStagingDir = 'N';
				getRequestDownloadList.send(params);
				refreshUploadFiles();
				this.addEventListener("refreshUploadFiles", onUploadFinished);
				getLinkedSampleFiles.send();
			}
			
			public function centerMe():void{
				PopUpManager.centerPopUp(this);
			}
			
			private function refreshFiles():void {
				expFileDict = new Dictionary();
				filesToRemove.removeAll();
				filesToUnlink.removeAll();
				linkSamplesTab.enabled = true;
				this.dirty.resetDirty();
				
				refreshUploadFiles();
				
				var params:Object = new Object();
				params.idRequest = idRequest;
				params.includeUploadStagingDir = 'N';
				getRequestDownloadList.send(params);
				
			}
			
			private function onGetExperimentUploadURL(event:ResultEvent):void {
				if (getExperimentUploadURL.lastResult.name() == "UploadExperimentURL") {
					var url:String = getExperimentUploadURL.lastResult.@url;
					initializeFileUploadControl(url);
				}
			}
			
			
			private function onFdtUploadStart(event:ResultEvent):void {
				if (fdtUploadStart.lastResult.name() == "FDTUploadUuid") {
					var uuid:String = fdtUploadStart.lastResult.@uuid;
					saveUUID.text = uuid;
					fdtStartButton.enabled = false;
					
					var url:URLRequest = new URLRequest('FastDataTransferUploadGetJnlpServlet.gx');
					var uv:URLVariables = new URLVariables();
					uv.uuid = uuid;
					url.data = uv;
					url.method = URLRequestMethod.POST;
					navigateToURL(url, '_self');				
				}
			}
			
			
			public function initializeFileUploadControl(uploadServletURL:String):void{
				
				if (multiFileUpload == null) {
					var postVariables:URLVariables = new URLVariables;
					postVariables.idRequest = idRequest;
					postVariables.requestNumber = requestNumber; 
					
					multiFileUpload = new MultiFileUpload(
						this.uploadFilesGrid,
						this.uploadBrowseButton,
						this.uploadClearButton,
						this.uploadDeleteButton,
						this.uploadButton,
						this.uploadProgressBar,
						uploadServletURL,
						postVariables,
						0,
						null
					);
					
					multiFileUpload.addEventListener(Event.COMPLETE, onUploadFinished);
					multiFileUpload.addEventListener(Event.OPEN, onChooseFileToUpload);
					
					this.uploadBrowseButton.enabled = true;
					this.uploadClearButton.enabled = true;
					this.uploadDeleteButton.enabled = true;
				} 
				
			}
			
			public function refreshUploadFiles():void {
				if(idRequest != null){  
					var params:Object = new Object();
					params.idRequest = idRequest;
					params.showUploads = "Y";
					getRequest.send(params);
				}
			}
			
			public function onUploadFinished(event:Event):void{
				this.theTab.selectedIndex = 1;
				refreshUploadFiles();
				refreshFiles();
			}
			
			private function onGetRequest(event:ResultEvent): void {
				if (getRequest.lastResult.name() == "OpenRequestList") {
					newFolderButton.enabled = true;
					for each(var fd:XML in uploadedFiles){
						expFileDict[String(fd.@displayName)] = fd;
					}
					constructExperimentFileList();
				}
			}
			
			private function onGetLinkedSampleFiles(event:ResultEvent): void {
				if (getLinkedSampleFiles.lastResult.name() == "SampleList") {
					sampleList = new XMLListCollection(getLinkedSampleFiles.lastResult..Sample);
				} else{
					mx.controls.Alert.show(getLinkedSampleFiles.lastResult..ACTMESSAGE.@TEXT);
				}
			}
			
			public function onChooseFileToUpload(event:Event):void {
			}
			
			
			private function onGetRequestDownloadList(event:ResultEvent): void {
				if (getRequestDownloadList.lastResult.name() != "RequestDownloadList") {
					mx.controls.Alert.show(getRequestDownloadList.lastResult..ACTMESSAGE.@TEXT);
					return;
				} else {
					recurseGetFileDescriptors(downloadList.children());
					constructExperimentFileList();
					callLater(expandDownloadTree);
				}  	 
			}
			
			private function recurseGetFileDescriptors(list:XMLList):void{
				if(list.length() == 0){
					return;
				}
				for each(var fd:XML in list){
					if(fd.name() == 'FileDescriptor'){
						expFileDict[String(fd.@displayName)] = fd.copy();
					} else if(fd.name() == 'RequestDownload'){
						recurseGetFileDescriptors(fd.children());
					}
					
				}
			}
			
			private function constructExperimentFileList():void{
				experimentFileList = new XMLListCollection();
				for (var key:Object in expFileDict){
					var found:Boolean = false;
					var fd:XML = expFileDict[key];
					for each(var s:XML in sampleList.children()){
						for each(var fdChild:XML in s.children()){
							if(fdChild.@displayName == fd.@displayName){
								found = true;
							}
						}
					}
					if(!found){
						experimentFileList.addItem(fd);
					}
				}
				
				sortExperimentFileList();
			}
			
			private function sortExperimentFileList():void{
				var sort:Sort = new Sort();
				sort.fields = [new SortField("@displayName", true)];
				experimentFileList.sort = sort;
				experimentFileList.refresh();
			}
			
			private function expandDownloadTree():void {
				var maxFiles:int = 60;
				var count:int = 0;
				if (downloadTree != null && downloadList.length > 0) {
					downloadTree.expandItem(downloadList.getItemAt(0),true); 
					for each(var a:Object in getRequestDownloadList.lastResult..Request.FileDescriptor) {
						downloadTree.expandItem(a,true);
						count++;
						if (count>=maxFiles){
							break;
						}
					}
				}
			}
			
			private function save():void {
				var params:Object = new Object();
				params.idRequest = idRequest;
				params.filesXMLString = downloadList.toXMLString();
				params.linkedSampleFileXMLString = sampleList.toXMLString();
				if (filesToRemove.length > 0) {
					params.filesToRemoveXMLString = filesToRemove.toXMLString();			
				}
				if (filesToUnlink.length > 0 ){
					params.filesToUnlinkXMLString = filesToUnlink.toXMLString();
				}
				organizeExperimentUploadFiles.send(params);
			}
			
			private function onOrganizeExperimentUploadFiles(event:ResultEvent): void {
				if (organizeExperimentUploadFiles.lastResult.name() != "SUCCESS") {
					mx.controls.Alert.show(organizeExperimentUploadFiles.lastResult..ACTMESSAGE.@TEXT);
				} else {
					refreshFiles();
					getLinkedSampleFiles.send();
				} 	 
			}
			private function getFilesGridRowNumber(item:Object,col:int):String
			{
				var x:int = filesGrid.dataProvider.getItemIndex(item) + 1;
				return String(x);
			} 		
			
			private function getDownloadName(item:Object):String {
				var info:String = item.name() == "RequestDownload" && item.hasOwnProperty("@info") && item.@info != "" ? " - " + item.@info : "";
				if (item.name() == "RequestDownload") {
					if (item.hasOwnProperty("@itemNumber") && item.@itemNumber != '') {
						var results:String = item.hasOwnProperty("@results") && item.@results != '' ? " - " + item.@results : '';
						return item.@itemNumber + results + info;
					} else {
						return item.@results + info;
					}
				} else {
					return item.@displayName + info;
				} 
			}
			
			public function showAddFolderWindow():void {
				if (this.downloadTree.selectedItem == null) {
					this.downloadTree.selectedIndex = 0;
				}
				
				var addFolderWindow:FolderAddWindow = FolderAddWindow(PopUpManager.createPopUp(DisplayObject(parentApplication.theBody), FolderAddWindow, true));
				PopUpManager.centerPopUp(addFolderWindow);
				addFolderWindow.uploadWindow = this;
				addFolderWindow.folderName.setFocus();
			}
			
			public function addDownloadFolder(folderName:String):void {
				
				var parent:XML = XML(downloadTree.selectedItem);
				if (this.downloadTree.selectedItem.@type != 'dir' && parent.name() != "Request" && parent.name() != "RequestDownload") {
					parent = XML(downloadTree.selectedItem).parent();
				} 
				
				var folder:XML = new XML("<RequestDownload isNew='Y' " +
					"key='X\tX\tX\t" + folderName + "' " +
					"itemNumber='" + folderName + "' " +
					"type='dir'" +
					"isEmpty='Y'" +
					"info='' />");
				if (parent.children().length() > 0) {
					var firstChild:XML = parent.children()[0];
					parent.insertChildBefore(firstChild, folder);	 		    	
				} else {
					parent.appendChild(folder);
				}	    	                         
				this.downloadTree.expandItem(parent, true);
				this.downloadTree.selectedItem = null;
				this.downloadTree.selectedItem = folder;
				this.downloadTree.scrollToIndex(this.downloadTree.selectedIndex); 
				this.downloadTree.selectedItem = null;
			}
			
			
			private function removeExperimentFile():void {
				if (this.filesGrid.selectedItems != null && this.filesGrid.selectedItems.length > 0) {
					for each(var uploadFile:XML in this.filesGrid.selectedItems) {
						this.filesToRemove.addItem(uploadFile);
						this.uploadedFiles.removeItemAt(uploadedFiles.getItemIndex(uploadFile));
					}
					linkSamplesTab.enabled = false;
					this.dirty.setDirty();
				} else if (this.downloadTree.selectedItems != null && this.downloadTree.selectedItems.length > 0) {
					for each(var downloadFile:XML in this.downloadTree.selectedItems) {
						// Only add to remove list if this file exists
						if (downloadFile.hasOwnProperty("@fileName")) {
							this.filesToRemove.addItem(downloadFile);
						}
						// Delete from XML hierarchy
						this.deleteNode(downloadFile);
						
					}
					
					linkSamplesTab.enabled = false;
					this.dirty.setDirty();
				}
			}
			
			private function onTabChange():void {
				if (theTab.selectedIndex == 1) {
					if (this.uploadedFiles == null || this.uploadedFiles.length == 0) {
						refreshFiles();
					}
				} else if (theTab.selectedIndex == 2){
					refreshFiles();
					refreshUploadFiles();
					getLinkedSampleFiles.send();
				}
			}
			
			
			private function onDragDropExpFile(event:DragEvent):void{
				event.preventDefault();
				//event.target.hideDropFeedback(event);
				//DragManager.showFeedback(DragManager.NONE);
				var dropNode:XML = null;
				
				if(samplesTree.selectedItem != null) {
					dropNode = samplesTree.selectedItem as XML;
					// Don't allow drag operation if nothing no drop node
					// found or we aren't dropping on a sample folder
					if (dropNode == null) {
						event.preventDefault();
						event.target.hideDropFeedback(event);
						DragManager.showFeedback(DragManager.NONE);
						Alert.show("You must drop files into a Seq Run Folder or onto a Sample");
						return;
					}
					
					if (dropNode.name() == "FileDescriptor") {
						dropNode = dropNode.parent();
					}
					var draggedItems:Array = null;		    
					if (event.dragInitiator.name == "samplesTree") {
						// For some reason, if we are dragging within the tree, the dragged
						// items has to be detected on the dragEnter event.  By the time
						// with get to the dragDrop event, the event.draggedItems contains
						// the drop target, not the dragged items.
						draggedItems = this.downloadDraggedItems;	    
					} else {
						var draggedFormat:String = event.dragSource.formats[0];
						draggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;	
					}
					
										
					if(dropNode.name() == 'Sample'){
						var seqRunNumberNode:XML = <SeqRunNumber />;
						if(dropNode.children().length() == 0){
							for each(var fd:XML in draggedItems){
								if(fd.name() == 'SeqRunNumber'){
									dropNode.appendChild(fd.copy());
								} else{
									seqRunNumberNode.appendChild(fd.copy());
								}
								if (event.dragInitiator.name == "samplesTree") {
									deleteNode(fd);
									samplesTree.selectedItem = null;
								} else{
									experimentFileList.removeItemAt(experimentFileList.getItemIndex(fd));
								}
							}
							dropNode.appendChild(seqRunNumberNode);
							samplesTree.expandItem(dropNode, true);
							samplesTree.expandItem(seqRunNumberNode, true);
						} else{
							for each(var fileDescript:XML in draggedItems){
								if(fileDescript.name() == 'SeqRunNumber'){
									dropNode.appendChild(fileDescript.copy());
									continue;
								}
								var droppedFileName:String = fileDescript.@displayName.replace(/_/gi, "");
								droppedFileName = droppedFileName.replace(/-/gi, "");
								droppedFileName = droppedFileName.substring(0, droppedFileName.indexOf(".") - 1);
								var isMatch:Boolean = false;
								
								for each(var file:XML in dropNode.children().children()){
									var currentFileName:String = file.@displayName.replace(/_/gi, "");
									currentFileName = currentFileName.replace(/-/gi, "");
									currentFileName = currentFileName.substring(0, currentFileName.indexOf(".") - 1);
								
									if(droppedFileName == currentFileName){
										file.parent().appendChild(fileDescript.copy());
										samplesTree.expandItem(dropNode, true);
										samplesTree.expandItem(file.parent(), true);
										isMatch = true;
										break;
									}
								}
								
								if(!isMatch){
									seqRunNumberNode.appendChild(fileDescript.copy());
									dropNode.appendChild(seqRunNumberNode);
									samplesTree.expandItem(dropNode, true);
									samplesTree.expandItem(seqRunNumberNode, true);
								}
								
								if (event.dragInitiator.name == "samplesTree") {
									deleteNode(fileDescript);
									samplesTree.selectedItem = null;
								} else{
									experimentFileList.removeItemAt(experimentFileList.getItemIndex(fileDescript));
								}
							}
						}
						event.target.hideDropFeedback(event);
						
					}
					
					if(dropNode.name() == 'SeqRunNumber'){
						if(dropNode.children().length() == 2){//Each seq run folder can have a max of 2 exp files
							Alert.show("A Seq Run Folder can only contain two Experiment Files.  Please remove one Experiment File from this folder and try again.");
							return;
						}
						for(var i:int = 0; i< draggedItems.length; i++){
							var dragItem:XML = XML(draggedItems[i]);
							dropNode.appendChild(dragItem.copy());
							if (event.dragInitiator.name == "samplesTree") {
								deleteNode(dragItem);
								samplesTree.selectedItem = null;
							} else{
								experimentFileList.removeItemAt(experimentFileList.getItemIndex(dragItem));
							}
							
						}
					}
					
					for each(var emptyfolder:XML in sampleList.children()){
						if(emptyfolder.children().length() == 0){
							deleteNode(emptyfolder);
						}
					}
					samplesTree.expandItem(dropNode, true);
					callLater(samplesTree.invalidateDisplayList);
					this.dirty.setDirty();
					event.target.hideDropFeedback(event);
				}
			}
			
			private function onDragDropFile(event:DragEvent) : void
			{
				
				// For some reason, preventDefault on AdvancedDataGrid
				// isn't enough to stop the defaut drop operation from
				// occurring. So I added hidDropFeedback and showFeedback
				// and that seemed to do the trick.	
				//this.downloadTree.hideDropFeedback(event);
				//event.preventDefault();
				//DragManager.showFeedback(DragManager.NONE);
				
				// determine where in the grid the drop occurs and select that node by the index; 
				// followed by retrieving the node itself. 
				var dropNode:XML = null;
				var dropFolderNode:XML = null;
				var siblingDropNode:XML = null;
				//var r:int = downloadTree.calculateDropIndex(event);
				//if (r > 0) { 
				//  r--;
				//}
				//downloadTree.selectedIndex = r;
				if(downloadTree.selectedItem != null) {
					dropNode = downloadTree.selectedItem as XML;
					
					// Don't allow drag operation if nothing no drop node
					// found
					if (dropNode == null) {
						event.preventDefault();
						event.target.hideDropFeedback(event);
						DragManager.showFeedback(DragManager.NONE);
						return;
					}
					
					dropFolderNode = dropNode;
					if (dropNode.name() == "FileDescriptor" && dropNode.@type != "dir") {
						dropFolderNode = dropNode.parent();
						siblingDropNode = dropNode;
					}
					// Don't allow drag operation if nothing no drop folder node
					// found
					if (dropFolderNode == null) {
						event.preventDefault();
						event.target.hideDropFeedback(event);
						DragManager.showFeedback(DragManager.NONE);
						return;
					}
					
					// get the drag format (its always items in our case
					// Get the dragged items
					var draggedItems:Array = null;		    
					if (event.dragInitiator.name == "downloadTree") {
						// For some reason, if we are dragging within the tree, the dragged
						// items has to be detected on the dragEnter event.  By the time
						// with get to the dragDrop event, the event.draggedItems contains
						// the drop target, not the dragged items.
						draggedItems = this.downloadDraggedItems;	    
					} else {
						var draggedFormat:String = event.dragSource.formats[0];
						draggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;	
					}
					for(var i:int = 0; i < draggedItems.length; i++) {
						var dragItem:XML = XML(draggedItems[i]);
						if (dragItem.name() == "FileDescriptor" || dragItem.name() == "RequestDownload") {
							var fileNode:XML;
							if (dragItem.name() == "FileDescriptor" ){
								fileNode = new XML("<FileDescriptor " +
									" displayName='" +  dragItem.@displayName + "'" + 
									" fileName='" +  dragItem.@fileName + "'" + 
									" type='" + dragItem.@type + "'" +
									" directoryName='" + dragItem.@directoryName + "'/>");
							}
							if (dragItem.name() == "RequestDownload" ){
								fileNode = new XML("<FileDescriptor " +
									" displayName='" +  dragItem.@itemNumber + "'" + 
									" fileName='" +  dragItem.@fileName + "'" + 
									" type='dir'" +
									" directoryName='" + dragItem.@directoryName + "'/>");
							}
							
							
							for each ( var child:Object in dragItem.children() ) {
								fileNode.appendChild(child);	
							}
							
							// Don't allow dropping on yourself (causes a cyclical error)
							if ( fileNode.@fileName == dropFolderNode.@fileName ){
								return;
							}
							
							if (siblingDropNode != null) {
								dropFolderNode.insertChildAfter(siblingDropNode, fileNode);	       				
							} else {
								dropFolderNode.appendChild(fileNode);
							}
							downloadTree.expandItem(dropFolderNode, true);
							
							// We have to manually remove the file when we are
							// dragging within the download tree.
							if (event.dragInitiator.name == "downloadTree") {
								deleteNode(dragItem);
								downloadTree.selectedItem = null;
							}
							linkSamplesTab.enabled = false;
							this.dirty.setDirty();
						} 
					}
				}
			}
			
			private function deleteNode(xmlToDelete:XML):Boolean {
				var cn:XMLList = XMLList(xmlToDelete.parent()).children();
				for ( var i:Number = 0 ; i < cn.length() ; i++ )
				{
					if ( cn[i] == xmlToDelete ) {
						delete cn[i];       
						return true;
					}
				}    
				return false;
			}
			
			private function doDragEnter(event:DragEvent):void
			{
				// Keep track of what was dragged.
				var draggedFormat:String = event.dragSource.formats[0];
				downloadDraggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;
				
				// Cancel default behaviour
				event.preventDefault();
				// Tell the DragManager that the Tree will accent the DragDrop
				DragManager.acceptDragDrop(Tree(event.target));
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				//downloadTree.showDropFeedback(event);
			}
			
			private function doDragOver(event:DragEvent):void
			{
				// Show the default "drop line" in the Tree control
				//downloadTree.showDropFeedback(event);
				// Cancel default behaviors
				
				var r:int = downloadTree.calculateDropIndex(event);
				if (r > 0) { 
					r--;
				}
				downloadTree.selectedIndex = r;
				var dropNode:XML = downloadTree.selectedItem as XML;
				
				var dropFolderNode:XML = dropNode;
				if (dropNode.name() == "FileDescriptor" && dropNode.@type != "dir") {
					dropFolderNode = dropNode.parent();
				}  else if (dropNode.name() == "Request" && r != 0) {
					if (dropNode.children().length() > 0) {
						dropFolderNode = dropNode.children()[0];
					} else {
						dropFolderNode = null;
					}
				} 
				
				if (dropFolderNode != null) {
					downloadTree.selectedItem = dropFolderNode;
				}
				
				event.preventDefault();
			}
			
			private function doDragExit(event:DragEvent):void
			{
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				downloadTree.hideDropFeedback(event);
			}
			
			private function doDragComplete(event:DragEvent):void
			{
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				downloadTree.hideDropFeedback(event);
			}
			
			//FUNCTIONS FOR SAMPLES TREE*********************************************************
			
			private function doDragEnterSamples(event:DragEvent):void
			{
				// Keep track of what was dragged.
				var draggedFormat:String = event.dragSource.formats[0];
				downloadDraggedItems = event.dragSource.dataForFormat(draggedFormat) as Array;
				
				// Cancel default behaviour
				event.preventDefault();
				
				DragManager.acceptDragDrop(AdvancedDataGrid(event.target));
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				samplesTree.showDropFeedback(event);
			}
			
			private function doDragEnterFiles(event:DragEvent):void{
				samplesTree.selectedItem = event.draggedItem;
				
			}
			
			private function doDragExitSamples(event:DragEvent):void {
				// hide the "drop line" that is shown in Tree control
				// when dropping in a Tree
				samplesTree.hideDropFeedback(event);
			}
			
			private function doDragOverSamples(event:DragEvent):void {
				// Show the default "drop line" in the Tree control
				samplesTree.showDropFeedback(event);
				// Cancel default behaviors
				
				var r:int = samplesTree.calculateDropIndex(event);
				if (r > 0) { 
					r--;
				}
				samplesTree.selectedIndex = r;
				var dropNode:XML = samplesTree.selectedItem as XML;
				
				if (dropNode.name() == "FileDescriptor" && dropNode.@type != "dir") {
					dropNode = dropNode.parent();
				}
				if (dropNode != null) {
					samplesTree.selectedItem = dropNode;
				}
				
				event.preventDefault();
			}
			
			public function showHTMLUploadForm():void {
				var url:URLRequest = new URLRequest('uploadExperimentFile.jsp?requestNumber=' + this.requestNumber);
				navigateToURL(url, '_blank');			
				this.theTab.selectedIndex = 1;
			}
			
			private function renameFile(event:MouseEvent):void{
				if((filesGrid.selectedItem == null && downloadTree.selectedItem == null) || downloadTree.selectedIndex == 0){
					return;
				}
					
				else{
					var renameWindow:RenameExperimentWindow = RenameExperimentWindow(mx.managers.PopUpManager.createPopUp(this, RenameExperimentWindow, true));
				}
				
				if(downloadTree.selectedItem != null){
					var selectedNode:XML = XML(downloadTree.selectedItem);
					if(selectedNode.name() == "RequestDownload"){
						renameWindow.oldExperimentName = selectedNode.@key;
					}
					else{
						renameWindow.oldExperimentName = downloadTree.selectedItem.@displayName;
					}
				}
				else{
					var filesGridNode:XML = XML(filesGrid.selectedItem);
					if(filesGridNode.name() == "RequestDownload"){
						renameWindow.oldExperimentName = filesGridNode.@key;
					}
					else{
						renameWindow.oldExperimentName = filesGrid.selectedItem.@displayName;
					}
				}
				var oldName:String = downloadTree.selectedItem.name() == "RequestDownload" ? downloadTree.selectedItem.@itemNumber : downloadTree.selectedItem.@displayName;
				renameWindow.oldName = oldName;
				renameWindow.fileList = this.downloadList;
				renameWindow.idRequest = this.idRequest;
				renameWindow.addEventListener("refreshUploadFiles", onUploadFinished);
				mx.managers.PopUpManager.centerPopUp(renameWindow);
				renameWindow.nameExperiment.setFocus();
			}
			
			private function closeMe():void{
				var params:Object = new Object();
				params.idRequest = request.@idRequest;
				params.idLab = request.@idLab;
				params.lastYear = 'Y';
				params.isMicroarray  = "N";
				params.isNextGenSeq  = "N";
				params.isBioanalyzer = "N";
				if(parentView.downloadView != null){
					parentView.downloadView.getRequestDownloadList.send(params);
				}
				PopUpManager.removePopUp(this);
			}
			
			public function startFDTUpload():void {			
				var params:Object = new Object();		
				params.idRequest = idRequest;
				
				fdtUploadStart.send(params);
			}
			
			private function getDisplayName(item:Object, column:Object):String {
				if (item.name() == "Sample") {
					return item.@name;
				} else{
					return '';
				} 
			}
			
			private function getSampleNumberOrFileName(item:Object, column:Object):String {
				if (item.name() == "Sample") {
					return item.@number;
				} else if(item.name() == "FileDescriptor"){
					return item.@displayName;
				} else{
					return '';
				} 
			}
			
			private function getTreeIcon(item:Object):Class{
				if(item.name() == "Sample"){
					return parentApplication.testTube;
				} else if(item.name() == "SeqRunNumber"  && item.children().length() > 0){
					return parentApplication.iconFolder;
				} else if(item.name() == "SeqRunNumber"){
					return parentApplication.iconFolderDisabled;
				} else{
					return null;
				}
				
			}
			
			private function createSeqRunFolder():void{
				var s:String = "Seq Run #";
				var seqRunNumberNode:XML = <SeqRunNumber />;
				samplesTree.selectedItem.appendChild(seqRunNumberNode);
				var x:int = 5;
			}
			
			private function swapFileOrder():void{
				var list:XMLList = samplesTree.selectedItem.children();
				list[0].@readID = "2";
				list[1].@readID = "1";
				var fileDescriptOne:XML = list[0];
				list[0] = list[1];
				list[1] = fileDescriptOne;
				samplesTree.selectedItem.setChildren(list);
				this.dirty.setDirty();
			}
			
			private function areSelectionsValid():void{
				if(samplesTree.selectedItems.length == 0){
					this.resetFileButton.enabled = false;
					this.swapFileOrderButton.enabled = false;
					
				} else if(samplesTree.selectedItems.length == 1 && samplesTree.selectedItem.name() == 'SeqRunNumber' && samplesTree.selectedItem.children().length() == 2){
					this.swapFileOrderButton.enabled = true;
					this.resetFileButton.enabled = false;
				} else{
					for each(var item:XML in samplesTree.selectedItems){
						if(item.name() != 'FileDescriptor'){
							this.resetFileButton.enabled = false;
							break;
						} else{
							this.resetFileButton.enabled = true;
							this.swapFileOrderButton.enabled = false;
						}
					}
				}
			}
			
			private function resetExpFileTree():void{
				for each(var item:XML in samplesTree.selectedItems){
					experimentFileList.addItem(item.copy());
					if(item.hasOwnProperty("@idExperimentFile")){
						filesToUnlink.addItem(item.copy());
					}
					
					deleteNode(item);
				}
				
				for each(var folder:XML in sampleList.children()){
					if(folder.children().length() == 0){
						deleteNode(folder);
					}
				}
				
				dirty.setDirty();
			}
			
			private function showFileNameToolTip(item:Object):String {
				if(item != null && item.hasOwnProperty("@displayName")){
					return item.@displayName;
				} else{
					return "";
				}
				
			}
			
		]]>
	</mx:Script>	
	
	<mx:XMLListCollection id="files"/>
	<mx:XMLListCollection id="filesToMove"/>
	<mx:XMLListCollection id="uploadedFiles" source="{getRequest.lastResult..RequestUpload.FileDescriptor}"/>
	<mx:XMLListCollection id="downloadList" source="{getRequestDownloadList.lastResult..Request}"/>
	<mx:XMLListCollection id="filesToRemove"/>
	<mx:XMLListCollection id="filesToUnlink"/>
	<mx:Canvas width="100%" height="100%">
		<mx:TextArea width="504" height="20" 
					 styleName="instructionBox" editable="false" wordWrap="true" visible="{theTab.selectedIndex == 1 || theTab.selectedIndex == 2}" text="Drag uploaded file into one of the folders on the right.To drag multiple files, hold CTRL or SHIFT key while selecting." right="0"/>
		<mx:TabNavigator id="theTab" change="onTabChange()"   width="100%" paddingTop="0" height="100%" styleName="form">
			<mx:VBox label="Upload files" width="100%" height="100%" verticalGap="2" paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4">
				<mx:ViewStack id="uploadFilesStack" width="100%" height="100%">
					<mx:VBox width="100%" height="100%">
						<mx:HBox height="20" width="100%">						
							<mx:LinkButton id="uploadBrowseButton" label="Choose files" icon="@Embed(source='../../assets/add.png')" disabledIcon="@Embed(source='../../assets/add_disable.png')"  toolTip="Choose file(s) to upload"  textDecoration="underline" enabled="false"/>
							<mx:LinkButton id="uploadDeleteButton" label="Remove" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')" toolTip="Remove file"  textDecoration="underline" enabled="false"/>
							<mx:LinkButton label="Clear all" toolTip="Clear all files" id="uploadClearButton" icon="@Embed(source='../../assets/cross.png')" disabledIcon="@Embed(source='../../assets/cross_disable.png')" textDecoration="underline" enabled="false"/>
							<mx:HBox width="100%"  horizontalAlign="right">
								<mx:VRule height="20"/>
								<mx:LinkButton visible="{parentApplication.isFDTSupported ? false : true}" label="Upload problems?  Try this." click="showHTMLUploadForm()" textDecoration="underline" icon="@Embed(source='../../assets/application_get.png')"/>
								
								<mx:LinkButton label="Upload and Download Help" 
											   click="parentApplication.showUploadDownloadHints()" 
											   id="buttondownloadhint"  icon="@Embed(source='../../assets/information.png')"/>							
							</mx:HBox>			          
						</mx:HBox>
						<mx:DataGrid id="uploadFilesGrid" width="100%" height="100%">				
						</mx:DataGrid>	
					</mx:VBox>
					<mx:VBox width="100%" height="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
						<mx:TextArea styleName="instructionBox" width="100%" wordWrap="true" height="100%" id="textarea1" fontSize="12">
							<mx:htmlText><![CDATA[This method of uploading files uses a Fast Data Transfer (FDT) server. To upload files:<br><br>     1. Select the "Start" button below.<br><br>     2. This will download a special "jnlp" file that can be opened using Java Web Start (either directly or after first saving<br>         the file to your desktop).<br><br>     3. Use the window launched by Java Web Start to select and upload the files to the server.<br><br>     4. After all of the files have been uploaded (% Completed column indicates 100 for all files), click the<br>        "Organize files" link on Experiment Detail to move the uploaded files to the appropriate folders.<br>         button.]]></mx:htmlText>
						</mx:TextArea>							
					</mx:VBox>
				</mx:ViewStack>
			</mx:VBox>
			<mx:VBox width="100%" height="100%" label="Organize files"  paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4" verticalGap="2">
				<mx:HBox width="100%" horizontalGap="2" paddingLeft="0">
					<mx:LinkButton id="newFolderButton" label="New folder"  enabled="false" disabledIcon="@Embed(source='../../assets/folder_add_disable.png')" icon="@Embed(source='../../assets/folder_add.png')" toolTip="Create folder"  textDecoration="underline" click="showAddFolderWindow()" paddingLeft="0"/>
					<mx:LinkButton id="removeFileButton" label="Remove (from file system)"  enabled="{filesGrid.selectedItem != null || (this.downloadTree.selectedItem != null)}" disabledIcon="@Embed(source='../../assets/delete_disable.png')" icon="@Embed(source='../../assets/delete.png')" toolTip="Remove file"  textDecoration="underline" click="removeExperimentFile()" paddingLeft="0"/>
					<mx:LinkButton id="renameFileButton" label="Rename" textDecoration="underline" paddingLeft="0" click="renameFile(event)" enabled="{(this.downloadTree.selectedItem != null) &amp;&amp; this.downloadTree.selectedIndex != 0}" toolTip="Rename selected file or folder" icon="@Embed(source='../../assets/segment_import.png')" disabledIcon="@Embed(source='../../assets/segment_import_disable.png')"/>
					<mx:Spacer width="100%"/>
					<mx:LinkButton id="refresh" label="Refresh" icon="@Embed(source='../../assets/refresh.png')" toolTip="Refresh files list"  textDecoration="underline" click="refreshFiles()"/>
					
				</mx:HBox>
				
				<mx:HDividedBox width="100%" height="100%">
					<mx:VBox width="300" height="100%" verticalGap="0" styleName="form" >
						<mx:HBox width="100%"  backgroundColor="#C9BEC0" backgroundAlpha="0.22" >
							<mx:Label text="Uploaded Files"/>
						</mx:HBox>
						<mx:DataGrid id="filesGrid"  
									 dragEnabled="true"
									 showHeaders="false"
									 allowMultipleSelection="true"
									 width="100%" height="100%"
									 dataProvider="{uploadedFiles}" dragMoveEnabled="true" itemClick="{this.downloadTree.selectedItem = null; this.renameFileButton.enabled = false}">
							<mx:columns>
								<mx:DataGridColumn headerText="Uploaded file"  dataField="@displayName"/>
							</mx:columns>
						</mx:DataGrid>
					</mx:VBox>
					<mx:VBox width="50%" height="100%" verticalGap="0" styleName="form">
						<mx:HBox width="100%"  backgroundColor="#C9BEC0" backgroundAlpha="0.22" >
							<mx:Label text="Experiment Folders"/>
						</mx:HBox>
						<mx:Tree id="downloadTree"
								 width="100%" height="100%" 
								 iconFunction="parentApplication.getTreeIcon" 
								 dataProvider="{downloadList}"
								 borderStyle="solid" 
								 labelFunction="{getDownloadName}"
								 dropEnabled="true"  		                
								 dragEnabled="true"
								 dragMoveEnabled="true"  
								 allowMultipleSelection="true"
								 showRoot="true"
								 dragDrop="onDragDropFile(event)"
								 dragOver="doDragOver(event)"
								 dragEnter="doDragEnter(event)"
								 dragExit="doDragExit(event)"
								 itemClick="{this.filesGrid.selectedItem = null;}">
						</mx:Tree>		
					</mx:VBox>
				</mx:HDividedBox>
			</mx:VBox>
			<mx:VBox width="100%" id="linkSamplesTab" toolTip="{linkSamplesTab.enabled == false ? 'Please save current changes to enable this tab' : ''}" height="100%" label="Link Samples"  paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4" verticalGap="2" >
				<mx:HBox width="100%" horizontalGap="2" paddingLeft="0" horizontalAlign="right"> 
					<mx:LinkButton id="newSeqRunFolder" label="New Folder"  enabled="{samplesTree.selectedIndex > -1 &amp;&amp; samplesTree.selectedItem.name() == 'Sample'}" disabledIcon="@Embed(source='../../assets/folder_add_disable.png')" icon="@Embed(source='../../assets/folder_add.png')" toolTip="Create a new folder for organizing experiment files"  textDecoration="underline" click="createSeqRunFolder()" paddingLeft="0"/>
					<mx:LinkButton id="swapFileOrderButton" label="Swap File Order" enabled="{false}" disabledIcon="@Embed(source='../../assets/refresh_disable.png')" icon="@Embed(source='../../assets/refresh.png')" toolTip="Swap Read 1 and Read 2 file order"  textDecoration="underline" click="swapFileOrder()" paddingLeft="0"/>
					<mx:LinkButton id="resetFileButton" label="Unlink" toolTip="Unlink selected files from samples and move them back to the left" enabled="{false}" icon="@Embed(source='../../assets/double_arrow_left.png')" disabledIcon="@Embed(source='../../assets/double_arrow_left_disabled.png')"  click="{resetExpFileTree(); sortExperimentFileList();}" textDecoration="underline" paddingLeft="0" />
					<mx:LinkButton id="refreshLinkage" label="Refresh" icon="@Embed(source='../../assets/refresh.png')" toolTip="Refresh files list"  textDecoration="underline" click="refreshFiles(); getLinkedSampleFiles.send();"/>
				</mx:HBox>
				<mx:HDividedBox width="100%" height="100%">
					<mx:VBox width="300" height="100%" verticalGap="0" styleName="form" >
						<mx:AdvancedDataGrid id="experimentFilesTree"  
											 dragEnabled="true"
											 allowMultipleSelection="true" dataTipFunction="showFileNameToolTip"
											 sortExpertMode="true"
											 sortableColumns="false"
											 width="100%" height="100%"
											 dragMoveEnabled="true" dropEnabled="false" itemClick="{this.samplesTree.selectedItem = null;}">
							<mx:dataProvider>
								<mx:HierarchicalData source="{experimentFileList}"/>
							</mx:dataProvider>	
							<mx:columns>
								<mx:AdvancedDataGridColumn headerText="Experiment Files" dataField="@displayName" showDataTips="true"/>
							</mx:columns>
						</mx:AdvancedDataGrid>
					</mx:VBox>
					<mx:VBox width="50%" height="100%" verticalGap="0" styleName="form">
						<mx:AdvancedDataGrid id="samplesTree" displayItemsExpanded="true"
											 width="100%" height="100%" 
											 dragDrop="onDragDropExpFile(event)"
											 dragOver="doDragOverSamples(event)"
											 dragExit="doDragExitSamples(event)"
											 dragEnter="doDragEnterSamples(event)"
											 iconFunction="getTreeIcon"
											 sortExpertMode="true"
											 sortableColumns="false"
											 borderStyle="solid" 
											 dropEnabled="true"  		                
											 dragEnabled="true" 
											 dragMoveEnabled="true"
											 allowMultipleSelection="true" dataTipFunction="showFileNameToolTip"
											 itemClick="{this.experimentFilesTree.selectedItem = null; areSelectionsValid();}">
							<mx:dataProvider>
								<mx:HierarchicalData source="{sampleList}"/>
							</mx:dataProvider>	
							<mx:columns>
								<mx:AdvancedDataGridColumn headerText="Sample ID" labelFunction="{getSampleNumberOrFileName}" width="300"/>
								<mx:AdvancedDataGridColumn headerText="Name" labelFunction="{getDisplayName}" showDataTips="true" />
								<mx:AdvancedDataGridColumn headerText="Date" dataField="@lastModifyDateDisplay"/>
								<mx:AdvancedDataGridColumn headerText="Size" dataField="@fileSizeText"/>
							</mx:columns>
						</mx:AdvancedDataGrid>
					</mx:VBox>
				</mx:HDividedBox>
			</mx:VBox>
		</mx:TabNavigator>
	</mx:Canvas>
	<mx:ControlBar width="100%">
		<mx:ViewStack id="buttonStack" width="100%" selectedIndex="{theTab.selectedIndex == 2 ? 1 : theTab.selectedIndex}">
			<mx:HBox width="100%">
				<mx:ViewStack id="buttonStack_1" width="100%" selectedIndex="{uploadFilesStack.selectedIndex}">
					<mx:HBox height="100%" width="100%">
						<mx:ProgressBar id="uploadProgressBar"  width="100%" labelPlacement="center" trackHeight="15" height="20" fontWeight="normal" themeColor="#EAED9A"/>
						<mx:Button label="Upload files" id="uploadButton"  enabled="false" visible="true" disabledIcon="@Embed(source='../../assets/upload_disable.png')" icon="@Embed(source='../../assets/upload.png')"/>	
					</mx:HBox>				 
					<mx:HBox height="100%" width="100%">
						<mx:Spacer  width="100%" height="10"/>	
						<mx:Label id="saveUUID" text="" visible="false"/>
						<mx:Button id="fdtStartButton" label="Start" click="startFDTUpload();"  enabled="true" visible="true"/>	      	
					</mx:HBox>		  			 
				</mx:ViewStack>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Spacer  width="100%" height="10"/>		
				<util:DirtyNote id="dirty"/>
				<mx:Button label="Save" id="saveButton"  enabled="true" click="save()" visible="true" icon="@Embed(source='../../assets/save.png')"/>	      	
				<mx:Button label="Close" enabled="true" click="{closeMe()}" visible="true" />	      	
			</mx:HBox>
		</mx:ViewStack>
	</mx:ControlBar>
	
</util:ResizableTitleWindow>
