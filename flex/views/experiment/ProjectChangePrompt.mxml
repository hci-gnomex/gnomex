<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
	title="Reassignment"
	showCloseButton="true" 
	width="490" 
	height="188"
	close="closeWindow('No')"
	xmlns:util="views.util.*" verticalScrollPolicy="off" horizontalScrollPolicy="off"
	layout="vertical" backgroundAlpha="100" defaultButton="{saveButton}">
	
	<mx:HTTPService             
		id="getLab" 
		url="GetLab.gx" 
		destination="getLab" 
		resultFormat="e4x"
		result="onGetLab(event)"
		fault="parentApplication.onFailHttpRequest('Failed to get lab', event)"
		method="POST" 
		useProxy="false">
	</mx:HTTPService> 
       
    <mx:Script>
	<![CDATA[
	
	import mx.collections.Sort;
	import mx.collections.XMLListCollection;
	import mx.controls.Alert;
	import mx.managers.PopUpManager;
	import mx.rpc.events.ResultEvent;
	
	import views.util.GNomExStringUtil;
		
	private var idLab:String;
	private var idBillingAccount:String;
	private var currentOwnerId:String;	
	private var idCoreFacility:String;
		
	[Bindable]
	private var changeBilling:Boolean;
		
	[Bindable]
	private var promptToManuallyChangeBilling:Boolean = false;
		
	[Bindable]
	private var appUsersForLab:XMLListCollection;
	private var appUserSort:Sort;
		
	private var navExperimentView:NavExperimentView;
		
		
	public	function init(navExperimentView:NavExperimentView, message:String, oldIdLab:String, idLab:String, currentOwnerId:String, idBillingAccount:String, isExternal:String, idCoreFacility:String, promptToManuallyChangeBilling:Boolean = false):void {
		this.navExperimentView = navExperimentView;
		this.message.text = message;
		this.idLab = idLab;
		this.currentOwnerId = currentOwnerId;
		this.idBillingAccount = idBillingAccount;
		this.idCoreFacility = idCoreFacility;
		this.promptToManuallyChangeBilling = promptToManuallyChangeBilling;
		if (isExternal != null && isExternal == "Y") {
			// External requests don't have billing accounts
			changeBilling = false;
		} else {
			// if not changing lab then keep old billing.
			if (oldIdLab != idLab) {
				changeBilling = true;
			} else {
				changeBilling = false;
			}
		}
		if (this.promptToManuallyChangeBilling) {
			changeBilling = false;
		}
		
		var params:Object = new Object();
		params.idLab = idLab;
		getLab.send(params);
	}		
		
	private function onGetLab(event:ResultEvent):void {	
		if (getLab.lastResult.name() == "OpenLabList") {		
			var lab:Object = getLab.lastResult..Lab.(@idLab == idLab);
			
			var emptyNode:XML = <BillingAccount idBillingAccount='' accountName=''/>;
			billingAccounts = new XMLListCollection(lab.billingAccounts.BillingAccount.(@isApproved == 'Y' && @isActive == 'Y' && @idCoreFacility == idCoreFacility));
			billingAccounts.addItemAt(emptyNode, 0);
			requestEditBillingAccountCombo.selectedItem = null;
			for each(var ba:Object in billingAccounts) {
				if (idBillingAccount != null && ba.@idBillingAccount == idBillingAccount) {
					requestEditBillingAccountCombo.selectedItem = ba;
					break;
				}
			}						
			
			emptyNode = <AppUser idAppUser='' displayName=''/>;
			appUsersForLab = new XMLListCollection(lab.members.AppUser.(@isActive=='Y'));
			for each (var mgr:Object in lab.managers.AppUser) {
				if (!appUsersForLab.contains(mgr)) {
					if(mgr.@isActive=='Y') {
						appUsersForLab.addItem(mgr);
					}
					
				}
			}
			if(appUsersForLab.length == 0) {
				Alert.show("Sorry, in order to reassign this experiment you must change its owner to a member of the new lab group. However, you do not have permission to access the member list for this lab. Please contact an administrator.");
				navExperimentView.onPromptToReassignProject("No", null, null);
				PopUpManager.removePopUp(this);
				return;
			}
			appUsersForLab.addItemAt(emptyNode, 0);
			
			appUserSort = new Sort();
			appUserSort.compareFunction = GNomExStringUtil.sortAppUsers;
			appUsersForLab.sort = appUserSort;
			appUsersForLab.refresh();
			ownerCombo.dataProvider = appUsersForLab;
			ownerCombo.dropdown.dataProvider = appUsersForLab;
			ownerCombo.selectedItem = emptyNode;
			for each(var a:Object in appUsersForLab) {
				if (a.@idAppUser == currentOwnerId) {
					ownerCombo.selectedItem = a;
					break;
				}
			}				
		}		
	}
		
		
    private function closeWindow(closeStatus:String):void {
		if(closeStatus == "Yes") {
			if (ownerCombo.selectedItem == null || ownerCombo.selectedIndex == 0) {
				ownerCombo.setFocus();
				Alert.show("Please select the experiment owner.");
				return;        		
			} 
			if (changeBilling && (requestEditBillingAccountCombo.selectedItem == null || requestEditBillingAccountCombo.selectedIndex == 0)) {
				requestEditBillingAccountCombo.setFocus();
				Alert.show("Please select the experiment billing account.");
				return;        		
			} 
			
		}
		navExperimentView.onPromptToReassignProject(closeStatus, ownerCombo.selectedItem.@idAppUser, requestEditBillingAccountCombo.selectedItem != null ? requestEditBillingAccountCombo.selectedItem.@idBillingAccount : null, promptToManuallyChangeBilling);
		PopUpManager.removePopUp(this);		
	}
		
	]]>
    </mx:Script>
	<mx:XMLListCollection id="billingAccounts" />
	
    <mx:VBox width="100%" height="100%" verticalGap="10"  verticalScrollPolicy="off" horizontalScrollPolicy="off">
		<mx:HBox width="100%" >
			<mx:TextArea id="message" editable="false" wordWrap="true" fontSize="11" width="100%" backgroundColor="#DDDDDD"/>
		</mx:HBox>				
		<mx:HBox width="100%">
			<mx:Label text="Owner" width="60"/>
			<util:ComboBoxDatProvChangeFix  width="409" id="ownerCombo" labelField="@displayName" dataProvider="{appUsersForLab}"></util:ComboBoxDatProvChangeFix>						         			
		</mx:HBox>	
		<mx:HBox width="100%" visible="{changeBilling}" includeInLayout="{changeBilling}">
			<mx:Label text="Account" width="60"/>
			<mx:ComboBox id="requestEditBillingAccountCombo"
						 labelField="@accountName" width="409"
						 dataProvider="{billingAccounts}" />
		</mx:HBox>
		<mx:HBox width="100%" visible="{promptToManuallyChangeBilling}" includeInLayout="{promptToManuallyChangeBilling}">
			<mx:Label text="This request is associated with multiple billing accounts, please adjust billing."/>
		</mx:HBox>
    </mx:VBox>

	<mx:ControlBar horizontalAlign="right" height="29">
		<mx:Button id="saveButton"  
			label="Yes"
			click="{closeWindow('Yes')}"/>
		<mx:Button label="No" click="{closeWindow('No')}"/>
	</mx:ControlBar>
</mx:TitleWindow>
