<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow
		xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:renderers="views.renderers.*"
		title="{'Add new data track to ' + parentName}"
		showCloseButton="true"
		width="458"
		close="closeWindow()"
		titleIcon="{parentApplication.iconDataTrackNew}"
		layout="vertical" backgroundAlpha="100" defaultButton="{saveButton}">

    <mx:HTTPService             
      id="saveDataTrack" 
      url="SaveDataTrack.gx"
      resultFormat="e4x"
      result="onSaveDataTrack(event)"
      fault="parentApplication.onFailHttpRequest('Failed to add data track', event)"
      method="POST"
      useProxy="false">
      <mx:request>
         <name>{dataTrackName.text}</name>
         <idGenomeBuild>{genomeBuild.@idGenomeBuild}</idGenomeBuild>
         <idDataTrackFolder>{idParentDataTrackFolder}</idDataTrackFolder>
         <codeVisibility>{visibilityCombo.selectedItem != null ? visibilityCombo.selectedItem.@value : ''}</codeVisibility>
         <idLab>{this.labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : ''}</idLab>
		 <idInstitution>{this.institutionCombo.selectedItem != null ? institutionCombo.selectedItem.@idInstitution : ''}</idInstitution>
      </mx:request>
    </mx:HTTPService> 
    


<mx:Script>
	<![CDATA[
	
	import mx.collections.XMLListCollection;
	import mx.controls.Alert;
	import mx.managers.PopUpManager;
	import mx.rpc.events.ResultEvent;

	import views.util.PromptXMLListCollection;
	
	
	[Bindable]
	public var idParentDataTrackFolder:Object = null;
	[Bindable]
	public var parentName:Object = null;
	[Bindable]
	private var genomeBuild:Object = null;
	
	
	[Bindable]
	public var idDataTrack:Object = null;

	private var parentView:DisplayObject = null;

	private var itemToSelect:XML;
		
	private	var visibilitySetByDefault:Boolean = false;
		
	[Bindable]
	private var possibleInstitutions:PromptXMLListCollection;

	public function init(genomeBuild:Object, parent:Object, parentView:DisplayObject, idLab:Object):void {
		this.parentView = parentView;
		this.genomeBuild = genomeBuild;
		if (parent != null) {
			this.idParentDataTrackFolder = parent.@idDataTrackFolder;
			parentName = parent.@label;			
		} else {
			parentName = genomeBuild.@name;
		}
		
		var idLab:Object = idLab;
		if (idLab == null) {
			if (!parentApplication.hasPermission('canWriteAnyObject') && 
				(parent != null && parent.@idLab != null && parent.@idLab != "")) {
				idLab = parent.@idLab;		
			}
		}
		
		var defaultVisibility:String = parentApplication.getProperty(parentApplication.PROPERTY_DEFAULT_VISIBILITY_DATATRACK);
		if (defaultVisibility != null && defaultVisibility.length > 0) {
			for each(var item:Object in parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry) {
				if (item.@value == defaultVisibility) {
					this.visibilityCombo.selectedItem = item;
					visibilitySetByDefault = true;
					break;
				}
			}			
		}
		

		// If the parent folder belongs to a user group or this is a non-admin
		// user, set the dataTrack
		// visibility to 'Members' and the user group combo
		// to the same user group.
		// Otherwise; set the visibility to 'PUBLIC'.
		if (idLab != null) {
			if(!visibilitySetByDefault) {
				for each(var o:Object in parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry) {
					if (o.@value == 'MEM') {
						this.visibilityCombo.selectedItem = o;
						break;
					}
				}				
			}

			for each (var g:Object in labCombo.dataProvider) {
				if (g.@idLab == idLab) {
					labCombo.selectedItem = g;
					break;
				}					
			}			
		} else {
			if(!visibilitySetByDefault) {
				for each(var v:Object in parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry) {
					if (v.@value == 'PUBLIC') {
						this.visibilityCombo.selectedItem = v;
						break;
					}
				}				
			}
			
		}

		setInstitutions();

	}
		
	private function visibilityChosen():void {
		if (this.labCombo.selectedItem != null && this.labCombo.selectedItem.@idLab != '' && this.visibilityCombo.selectedItem != null && this.visibilityCombo.selectedItem.@value == "INST") {
			var lab:Object = labCombo.selectedItem;
			this.institutionCombo.enabled = true;
			this.institutionLabel.enabled = true;
			if (this.institutionCombo.selectedItem == null || this.institutionCombo.selectedItem.@idInstitution == '') {
				for each(var inst:Object in this.possibleInstitutions) {
					if (inst.@idInstitution == lab.@defaultIdInstitutionForLab) {
						this.institutionCombo.selectedItem = inst;
						break;
					}
				}
			}
		} else {
			this.institutionCombo.enabled = false;
			this.institutionCombo.selectedItem = null;
			this.institutionLabel.enabled = false;
		}
	}

	private function setInstitutions():void {
		if (labCombo.selectedItem != null) {
			// Set up institutions
			var emptyInstNode:XML = <Institution idInstitution='' display=''/>;
			possibleInstitutions = new PromptXMLListCollection(emptyInstNode, new XMLListCollection(labCombo.selectedItem.institutions.Institution.(@isActive == 'Y')));
			this.institutionCombo.dataProvider = possibleInstitutions;
			visibilityChosen();
		}
	}

	private function onLabChosen():void {
		if(!visibilitySetByDefault) {
			if (this.labCombo.selectedItem != null && this.labCombo.selectedItem.@idLab != "") {
				for each(var item:Object in parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry) {
					if (item.@value == 'MEM') {
						this.visibilityCombo.selectedItem = item;
						break;
					}
				}
			}
		}
		
		setInstitutions();
	}
				
	

   	private function onSaveDataTrack(event:ResultEvent):void {
		if (saveDataTrack.lastResult.name() == "SUCCESS") {			
			closeWindow();
			itemToSelect = new XML(
			   "<DataTrack " +
			     " idDataTrack='" + saveDataTrack.lastResult.@idDataTrack + "'" +
			     " idDataTrackFolder='" + saveDataTrack.lastResult.@idDataTrackFolder  + "'" +
			     " idGenomeBuild='" + genomeBuild.@idGenomeBuild  + "'" +
				 " idLab='" + (labCombo.selectedItem != null ? labCombo.selectedItem.@idLab : '') + "'" +
			    "/>");
			if (parentView is NavDataTrackView) {
				NavDataTrackView(parentView).refreshDataTracksAndSelect(itemToSelect);
			} else if (parentView is DistributeDataTrackWindow) {
				DistributeDataTrackWindow(parentView).refreshDataTracksAndSelect(itemToSelect);
			}
			
			
		}else {
			Alert.show(saveDataTrack.lastResult..ACTMESSAGE.@TEXT);
		}		
	}

    private function closeWindow():void {
		PopUpManager.removePopUp(this);		
	}
	
	private function save():void {
		if (this.saveButton.label == 'Save') {
			saveDataTrack.send()
		}
	}
		
	]]>
</mx:Script>
    
	<mx:VBox id="mainBody" styleName="form" width="100%" height="100%" paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4">
		<mx:VBox width="100%" height="100%" id="vbox1" verticalGap="4">
			
			<mx:VBox  width="100%" verticalGap="0">
				<mx:Label text="Name" width="100%" fontWeight="normal" id="label2" styleName="formLabel"/>
				<mx:HBox width="100%">
					<mx:TextArea id="dataTrackName"  width="432" maxChars="2000"/>
				</mx:HBox>
				
			</mx:VBox>
			
			<mx:VBox width="100%" verticalGap="0">
				<mx:Label text="Lab" fontWeight="normal" styleName="formLabel">
				</mx:Label>
				<renderers:FilterComboBox id="labCombo" dataProvider="{parentApplication.submitRequestLabList}" width="432" labelField="@name" change="{onLabChosen()}"/>
			</mx:VBox>
			<mx:VBox width="100%" verticalGap="0">
				<mx:Label text="Visibility" width="100%" fontWeight="normal" id="label3" styleName="formLabel"/>
				<mx:HBox width="100%" id="visibilityBox">
					<mx:ComboBox id="visibilityCombo" dataProvider="{parentApplication.dictionaryManager.xml.Dictionary.(@className == 'hci.gnomex.model.Visibility').DictionaryEntry}" labelField="@display" width="432" close="{visibilityChosen()}"/>
				</mx:HBox>
				<mx:Label text="Institution" width="100%" fontWeight="normal" id="institutionLabel" styleName="formLabel" paddingTop="4"/>
				<mx:ComboBox id="institutionCombo" labelField="@display" width="432"/>
				<mx:Text id="institutionNote" styleName="submitNote" visible="{this.visibilityCombo.selectedItem.@display == 'Institution' &amp;&amp; this.labCombo.selectedItem..institutions.children().length() == 0 }" paddingBottom="0" includeInLayout="{this.visibilityCombo.selectedItem.@display == 'Institution' &amp;&amp; this.labCombo.selectedItem..institutions.children().length() == 0 }">									
					<mx:text>Institution visibility can only be set when lab belongs to an institution.</mx:text>
				</mx:Text>
			</mx:VBox>
			
		</mx:VBox>
		
		
		
	</mx:VBox>
	
	<mx:ControlBar horizontalAlign="right">
		<mx:Button id="saveButton"  
				   enabled="{dataTrackName.text != null &amp;&amp; dataTrackName.text != '' &amp;&amp; !institutionNote.visible &amp;&amp; visibilityCombo.selectedItem != null &amp;&amp; visibilityCombo.selectedItem.@value != '' &amp;&amp; (visibilityCombo.selectedItem.@value == 'PUBLIC' || (labCombo.selectedItem != null &amp;&amp; labCombo.selectedItem.@idLab != '')) ? true : false}" 
				   icon="{parentApplication.iconSave}" 
				   disabledIcon="{parentApplication.iconSaveDisabled}" 
				   label="Save"
				   click="{save()}"/>
		<mx:Button id="cancelButton" label="Cancel" click="closeWindow()"/>
	</mx:ControlBar>
	
	
	
</mx:TitleWindow>
